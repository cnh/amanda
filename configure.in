dnl Process this file with autoconf to produce a configure script.

AC_INIT(common-src/amanda.h)
AC_CANONICAL_SYSTEM
AM_INIT_AUTOMAKE(amanda, 2.3.0.4)
AM_CONFIG_HEADER(config.h)

AC_PREREQ(2.12)			dnl Minimum Autoconf version required.

dnl host system/type
AC_ARG_PROGRAM

if test -f config.local; then
    echo "running local script ./config.local"
    . ./config.local
fi

dnl
dnl Set the version number of this release of Amanda from the VERSION
dnl string, which is set in AM_INIT_AUTOMAKE.
dnl
changequote(,)
VERSION_MAJOR=`expr "$VERSION" : '\([0-9]*\)'`
VERSION_MINOR=`expr "$VERSION" : '[0-9]*\.\([0-9]*\)'`
VERSION_PATCH=`expr "$VERSION" : '[0-9]*\.[0-9]*\.\([0-9]*\)'`
VERSION_COMMENT=\"`expr "$VERSION" : '[0-9]*\.[0-9]*\.[0-9]*\(.*\)'`\"
changequote([,])

VERSION_SUFFIX="$VERSION"
AC_SUBST(VERSION_MAJOR)
AC_SUBST(VERSION_MINOR)
AC_SUBST(VERSION_PATCH)
AC_SUBST(VERSION_COMMENT)
AC_SUBST(VERSION_SUFFIX)

dnl
dnl runtime and compile time
dnl
SYSPATH="/bin:/usr/bin:/sbin:/usr/sbin:/usr/ucb:/usr/bsd:/etc:/usr/etc"
LOCPATH=`(
    test "x$prefix" = xNONE && prefix=$ac_default_prefix
    test "x$exec_prefix" = xNONE && exec_prefix=${prefix}
    eval echo "$libexecdir:$PATH:/usr/local/bin"
)`
SYSLOCPATH="$SYSPATH:$LOCPATH"
LOCSYSPATH="$LOCPATH:$SYSPATH"

AC_ARG_WITH(includes,
    [   --with-includes=DIR    site header files for readline, etc in DIR],
    [
	for dir in $withval; do
	    if test -d "$dir"; then
		AMANDA_CPPFLAGS="$AMANDA_CPPFLAGS -I$dir"
	    else
		AC_MSG_WARN([*** Include directory $dir does not exist.])
	    fi
	done
    ],
    AMANDA_CPPFLAGS=
)

AC_ARG_WITH(rundump,
    [   --with-rundump         use rundump (setuid-root) to invoke dump],
    [
        if test "$withval" = "yes"; then
            AC_DEFINE(USE_RUNDUMP)
        fi
    ])

AC_ARG_WITH(libraries,
    [   --with-libraries=DIR   site library directories for readline, etc in DIR],
    [
	for dir in $withval; do
	    if test -d "$dir"; then
		AMANDA_LDFLAGS="$AMANDA_LDFLAGS -L$dir"
	    else
		AC_MSG_WARN([*** Library directory $dir does not exist.])
	    fi
	done
    ],
    AMANDA_LDFLAGS=
)

AC_ARG_WITH(configdir,
    [   --with-configdir=DIR   runtime config files in DIR [sysconfdir/amanda]],
    [
	if test "$withval"; then
	    CONFIG_DIR="$withval"
	else
	    AC_MSG_WARN([*** You must supply an argument to the --with-configdir option.])
	fi
    ]
)
: ${CONFIG_DIR:=$sysconfdir/amanda}
CONFIG_DIR=`(
    test "x$prefix" = xNONE && prefix=$ac_default_prefix
    test "x$exec_prefix" = xNONE && exec_prefix=${prefix}
    eval echo "$CONFIG_DIR"
)`
AC_DEFINE_UNQUOTED(CONFIG_DIR,"$CONFIG_DIR")
AC_SUBST(CONFIG_DIR)

AC_ARG_WITH(indexdir,
    [   --with-indexdir=DIR    index of files dumped go in DIR [localstatedir/amanda-index]],
    [
	if test "$withval"; then
	    INDEX_DIR="$withval"
	else
	    AC_MSG_WARN([*** You must supply an argument to the --with-indexdir option.])
	fi
    ]
)
: ${INDEX_DIR:=$localstatedir/amanda-index}
INDEX_DIR=`(
    test "x$prefix" = xNONE && prefix=$ac_default_prefix
    test "x$exec_prefix" = xNONE && exec_prefix=${prefix}
    eval echo "$INDEX_DIR"
)`
AC_DEFINE_UNQUOTED(INDEX_DIR,"$INDEX_DIR")
AC_SUBST(INDEX_DIR)

AC_ARG_WITH(dbdir,
    [   --with-dbdir=DIR       curinfo database in DIR [localstatedir/amanda]],
    [
	if test "$withval"; then
	    DB_DIR="$withval"
	else
	    AC_MSG_WARN([*** You must supply an argument to the --with-dbdir option.])
	fi
    ]
)
: ${DB_DIR:=$localstatedir/amanda}
DB_DIR=`(
    test "x$prefix" = xNONE && prefix=$ac_default_prefix
    test "x$exec_prefix" = xNONE && exec_prefix=${prefix}
    eval echo "$DB_DIR"
)`
AC_DEFINE_UNQUOTED(DB_DIR,"$DB_DIR")
AC_SUBST(DB_DIR)

AC_ARG_WITH(logdir,
    [   --with-logdir=DIR      log files go in DIR [localstatedir/amanda]],
    [
	if test "$withval"; then
	    LOG_DIR="$withval"
	else
	    AC_MSG_WARN([*** You must supply an argument to the --with-logdir option.])
	fi
    ]
)
: ${LOG_DIR:=$localstatedir/amanda}
LOG_DIR=`(
    test "x$prefix" = xNONE && prefix=$ac_default_prefix
    test "x$exec_prefix" = xNONE && exec_prefix=${prefix}
    eval echo "$LOG_DIR"
)`
AC_DEFINE_UNQUOTED(LOG_DIR,"$LOG_DIR")
AC_SUBST(LOG_DIR)

AC_ARG_WITH(suffixes,
    [   --with-suffixes        install binaries with version string appended to name],
    USE_VERSION_SUFFIXES=yes
)
: ${USE_VERSION_SUFFIXES:=no}
if test "$USE_VERSION_SUFFIXES" = yes; then
    AC_DEFINE(USE_VERSION_SUFFIXES)

    program_suffix="-$VERSION"
    # This is from the output of configure.in.
    if test "$program_transform_name" = s,x,x,; then
	program_transform_name=
    else
	# Double any \ or $.  echo might interpret backslashes.
	cat <<\EOF_SED > conftestsed
s,\\,\\\\,g; s,\$,$$,g
EOF_SED
	program_transform_name="`echo $program_transform_name|sed -f conftestsed`"
	rm -f conftestsed
    fi
    test "$program_prefix" != NONE &&
	program_transform_name="s,^,${program_prefix},; $program_transform_name"
    # Use a double $ so make ignores it.
    test "$program_suffix" != NONE &&
	program_transform_name="s,\$\$,${program_suffix},; $program_transform_name"
 
    # sed with no file args requires a program.
    test "$program_transform_name" = "" && program_transform_name="s,x,x,"
else
    USE_VERSION_SUFFIXES=no
fi
AC_SUBST(USE_VERSION_SUFFIXES)

BUILD_CHANGER_PROGS_LIBEXEC="seagate-changer"
BUILD_CHANGER_SCRIPTS_LIBEXEC="chg-generic hp-changer no-changer rth-changer"
BUILD_CLIENT_PROGS_LIBEXEC="amandad calcsize createindex-dump createindex-gnutar rundump runtar selfcheck sendbackup-dump sendbackup-gnutar sendindex sendsize"
BUILD_CLIENT_SCRIPTS_LIBEXEC="patch-system"
BUILD_SERVER_PROGS_BIN="amadmin amcheck amflush amlabel amtape"
BUILD_SERVER_PROGS_LIBEXEC="amgetidx amidxtaped amindexd amtrmidx driver dumper getconf planner reporter taper"
BUILD_SERVER_SCRIPTS_BIN="amcheckdb amcleanup amdump amoverview amrmtape amtoc amverify"
case "$target" in
    *-hp-*)
	BUILD_CLIENT_SCRIPTS_BIN=amhpfixdevs
	;;
    *)
	BUILD_CLIENT_SCRIPTS_BIN=
	;;
esac

AC_SUBST(BUILD_CHANGER_PROGS_LIBEXEC)
AC_SUBST(BUILD_CHANGER_SCRIPTS_LIBEXEC)
AC_SUBST(BUILD_CLIENT_SCRIPTS_BIN)
AC_SUBST(BUILD_CLIENT_PROGS_LIBEXEC)
AC_SUBST(BUILD_SERVER_PROGS_BIN)
AC_SUBST(BUILD_SERVER_PROGS_LIBEXEC)
AC_SUBST(BUILD_SERVER_SCRIPTS_BIN)
AC_SUBST(BUILD_CLIENT_SCRIPTS_LIBEXEC)

AC_ARG_WITH(client-only,
    [   --with-client-only     build Amanda for a client only machine],
    [
	BUILD_CHANGER_PROGS_LIBEXEC=
	BUILD_CHANGER_SCRIPTS_LIBEXEC=
	BUILD_SERVER_PROGS_BIN=
	BUILD_SERVER_PROGS_LIBEXEC=
	BUILD_SERVER_SCRIPTS_BIN=
    ]
)

AC_ARG_WITH(server-only,
    [   --with-server-only     build Amanda for a server only machine],
    [
	BUILD_CLIENT_PROGS_LIBEXEC=
	BUILD_CLIENT_SCRIPTS_LIBEXEC=
    ]
)

AC_ARG_WITH(server,
   [   --with-server=HOST     default amanda index server [`uname -n`]],
   [
	if test "$withval"; then
	    DEFAULT_SERVER="$withval"
	else
	    AC_MSG_WARN([*** You must supple an an argument to the --with-server option.])
	fi
    ]
)
: ${DEFAULT_SERVER:=`uname -n`}
AC_DEFINE_UNQUOTED(DEFAULT_SERVER,"$DEFAULT_SERVER")
AC_SUBST(DEFAULT_SERVER)

AC_ARG_WITH(dont-force-uid,
    [   --with-dont-force-uid  do not force the uid to --with-user],
    FORCE_USERID=no
)
: ${FORCE_USERID:=yes}
if test "$FORCE_USERID" = yes; then
    AC_DEFINE(FORCE_USERID)
fi

AC_ARG_WITH(user,
    [   --with-user=USER       force execution to USER on client systems [amanda]],
    [
	if test "$withval"; then
	    CLIENT_LOGIN="$withval"
	else
	    AC_MSG_WARN([*** You must supply an argument to the --with-user option.])
	fi
    ]
)
: ${CLIENT_LOGIN:=amanda}
AC_DEFINE_UNQUOTED(CLIENT_LOGIN,"$CLIENT_LOGIN")
AC_SUBST(CLIENT_LOGIN)

AC_ARG_WITH(group,
    [   --with-group=USER      group allowed to execute setuid-root programs [`(id 2>/dev/null; echo amanda) | sed -e 's/^.*gid=[0-9]*(\([^)]*\)).*/\1/' | grep -v = | head -1`]],
    [
	if test "$withval"; then
	    SETUID_GROUP="$withval"
	else
	    AC_MSG_WARN([*** You must supply an argument to the --with-group option.])
	fi
    ]
)
[: ${SETUID_GROUP:=`(id 2>/dev/null; echo amanda) | sed -e 's/^.*gid=[0-9]*(\([^)]*\)).*/\1/' | grep -v = | head -1`}]
AC_SUBST(SETUID_GROUP)

AC_ARG_WITH(config,
   [   --with-config=CONFIG   default configuration [DailySet1]],
   [
	if test "$withval"; then
	    DEFAULT_CONFIG="$withval"
	else
	    AC_MSG_WARN([*** You must supply an argument to the --with-config option.])
	fi
   ]
)
: ${DEFAULT_CONFIG:=DailySet1}
AC_DEFINE_UNQUOTED(DEFAULT_CONFIG,"$DEFAULT_CONFIG")
AC_SUBST(DEFAULT_CONFIG)

AC_ARG_WITH(tape-server,
    [   --with-tape-server=HOST default restoring tape server is HOST [--with-server]],
    [
	if test "$withval"; then
	    DEFAULT_TAPE_SERVER="$withval"
	else
	    AC_MSG_WARN([*** You must supply an argument to the --with-tape-server option.])
	fi
    ]
)
: ${DEFAULT_TAPE_SERVER:=$DEFAULT_SERVER}
AC_DEFINE_UNQUOTED(DEFAULT_TAPE_SERVER,"$DEFAULT_TAPE_SERVER")
AC_SUBST(DEFAULT_TAPE_SERVER)

AC_ARG_WITH(tape-device,
    [   --with-tape-device=ARG restoring tape server HOST's no rewinding tape drive],
    [
	if test "$withval"; then
	    DEFAULT_TAPE_DEVICE="$withval"
	else
	    AC_MSG_WARN([*** You must supply an argument to the --with-tape-device option.])
	fi
    ]
)
: ${DEFAULT_TAPE_DEVICE:=/dev/null}
AC_DEFINE_UNQUOTED(DEFAULT_TAPE_DEVICE,"$DEFAULT_TAPE_DEVICE")
AC_SUBST(DEFAULT_TAPE_DEVICE)

AC_ARG_WITH(rew-tape-drive,
    [   --with-rew-tape=ARG    rewinding tape device for changer scripts is ARG],
    [
	if test "$withval"; then
	    TAPE_REWIND_DEVICE="$withval"
	else
	    AC_MSG_WARN([*** You must supply an argument to the --with-re-tape-drive option.])
	fi
    ]
)
: ${TAPE_REWIND_DEVICE:=/dev/null}
AC_DEFINE_UNQUOTED(TAPE_REWIND_DEVICE,"$TAPE_REWIND_DEVICE")
AC_SUBST(TAPE_REWIND_DEVICE)

AC_ARG_WITH(norew-tape-drive,
    [   --with-norew-tape=ARG  norewinding tape device for changer scripts is ARG],
    [
	if test "$withval"; then
	    TAPE_NO_REWIND_DEVICE="$withval"
	else
	    AC_MSG_WARN([*** You must supple an argument to the --with-norew-tape-drive option.])
	fi
    ]
)
: ${TAPE_NO_REWIND_DEVICE:=/dev/null}
AC_DEFINE_UNQUOTED(TAPE_NO_REWIND_DEVICE,"$TAPE_NO_REWIND_DEVICE")
AC_SUBST(TAPE_NO_REWIND_DEVICE)    

AC_ARG_WITH(fqdn,
    [   --with-fqdn            use FQDN's to backup multiple networks],
    USE_FQDN=yes
)
: ${USE_FQDN:=no}
if test "$USE_FQDN" = yes; then
    AC_DEFINE(USE_FQDN)
fi

AC_ARG_WITH(smbclient,
    [   --with-smbclient[=PATH] Compile in Samba PC backup support, where PATH is the location of smbclient],
    [
	if test "$withval"; then
	    SAMBA_CLIENT="$withval"
	else
	    AC_PATH_PROGS(SAMBA_CLIENT,smbclient,,$LOCSYSPATH)
	fi
	if test -z "$SAMBA_CLIENT"; then
	    AC_MSG_WARN([*** You requested Samba support but you did not supply the location])
	    AC_MSG_WARN([*** of smbclient to --with-smbclient and configure was unable to find])
	    AC_MSG_WARN([*** smbclient on your system.])
	fi
    ]
)
if test "$SAMBA_CLIENT"; then
    LIBOBJS="$LIBOBJS findpass.o"
    AC_DEFINE_UNQUOTED(SAMBA_CLIENT,"$SAMBA_CLIENT")
fi

AC_ARG_WITH(gnutar-exclude,
    [   --with-gnutar-exclude  have gnutar exclude files from dumps],
    GNUTAR_EXCLUDE_FILE=yes
)
: ${GNUTAR_EXCLUDE_FILE:=no}
if test "$GNUTAR_EXCLUDE_FILE" = yes; then
    AC_DEFINE(GNUTAR_EXCLUDE_FILE)
fi

AC_ARG_WITH(gnutar-listed-incremental,
   [   --with-gnutar-listed-incremental=DIR  gnutar directory lists go in DIR [localstatedir/amanda-gnutar-lists]],
   [
	case "$withval" in
	    n | no)		unset GNUTAR_LIST_DIR ;;
	    y | ye | yes)	: ${GNUTAR_LIST_DIR:=$localstatedir/amanda-gnutar-lists} ;;
	    *)			: ${GNUTAR_LIST_DIR:="$withval"} ;;
	esac
    ]
)
: ${GNUTAR_LIST_DIR:=$localstatedir/amanda-gnutar-lists}
(
	tmp=`eval echo "$GNUTAR_LIST_DIR"`
	if test "$tmp"; then
	    AC_DEFINE_UNQUOTED(GNUTAR_LISTED_INCREMENTAL_DIR,"$tmp")
        fi
)

AC_ARG_ENABLE(gnutar-atime-preserve,
    [   --enable-gnutar-atime-preserve  gnutar preserves atime, do not use],
    GNUTAR_ATIME_PRESERVE=$enableval
)
: ${GNUTAR_ATIME_PRESERVE=no}
case "$GNUTAR_ATIME_PRESERVE" in
    n | no) : ;;
    y | ye | yes) 
	AC_DEFINE(ENABLE_GNUTAR_ATIME_PRESERVE)
    ;;
esac

AC_ARG_WITH(bsd-security,
    [   --with-bsd-security    use BSD rsh/rlogin style security],
    BSD_SECURITY=yes
)
: ${BSD_SECURITY:=no}
if test "$BSD_SECURITY" = yes; then
    AC_DEFINE(BSD_SECURITY)
fi

AC_ARG_WITH(amandahosts,
    [   --with-amandahosts     use .amandahosts instead .rhosts],
    USE_AMANDAHOSTS=yes
)
: ${USE_AMANDAHOSTS:=no}
if test "$USE_AMANDAHOSTS" = yes; then
    AC_DEFINE(USE_AMANDAHOSTS)
fi

AC_ARG_WITH(krb4-security,
    [   --with-krb4-security   use Kerbose 4 security and set the following:],
    KRB4_SECURITY=yes
)
: ${KRB4_SECURITY:=no}
if test "$KRB4_SECURITY" = yes; then
    AC_DEFINE(KRB4_SECURITY)
fi

AC_ARG_WITH(server-principal,
    [     --with-server-principal=ARG    server host principal  ["amanda"]],
    [
	if test "$withval"; then
	    SERVER_HOST_PRINCIPLE="$withval"
	else
	    AC_MSG_WARN([*** You must supply an argument to the --with-server-principal option.])
	fi
    ]
)
: ${SERVER_HOST_PRINCIPLE:="amanda"}
AC_DEFINE_UNQUOTED(SERVER_HOST_PRINCIPLE,"$SERVER_HOST_PRINCIPLE")

AC_ARG_WITH(server-instance,
    [     --with-server-instance=ARG     server host instance   ["amanda"]],
    [
	if test "$withval"; then
	    SERVER_HOST_INSTANCE="$withval"
	else
	    AC_MSG_WARN([*** You must supply an argument to the --with-server-instance option.])
	fi
    ]
)
: ${SERVER_HOST_INSTANCE:="amanda"}
AC_DEFINE_UNQUOTED(SERVER_HOST_INSTANCE,"$SERVER_HOST_INSTANCE")

AC_ARG_WITH(server-keyfile,
    [     --with-server-keyfile=ARG      server host key file   ["/.amanda"]],
    [
	if test "$withval"; then
	    SERVER_HOST_KEY_FILE="$withval"
	else
	    AC_MSG_WARN([*** You must supply an argument to the --with-server-keyfile option.])
	fi
    ]
)
: ${SERVER_HOST_KEY_FILE:="/.amanda"}
AC_DEFINE_UNQUOTED(SERVER_HOST_KEY_FILE,"$SERVER_HOST_KEY_FILE")

AC_ARG_WITH(client-principal,
    [     --with-client-principal=ARG    client host principal  ["rcmd"]],
    [
	if test "$withval"; then
	    CLIENT_HOST_PRINCIPLE="$withval"
	else
	    AC_MSG_WARN([*** You must supply an argument to the --with-client-principal option.])
	fi
    ]
)
: ${CLIENT_HOST_PRINCIPLE:="rcmd"}
AC_DEFINE_UNQUOTED(CLIENT_HOST_PRINCIPLE,"$CLIENT_HOST_PRINCIPLE")

AC_ARG_WITH(client-instance,
    [     --with-client-instance=ARG     client host instance   [HOSTNAME_INSTANCE]],
    [
	if test "$withval"; then
	    CLIENT_HOST_INSTANCE="$withval"
	else
	    AC_MSG_WARN([*** You must supply an argument to the --with-client-instance option.])
	fi
    ]
)
: ${CLIENT_HOST_INSTANCE:=HOSTNAME_INSTANCE}
AC_DEFINE_UNQUOTED(CLIENT_HOST_INSTANCE,$CLIENT_HOST_INSTANCE)

AC_ARG_WITH(client-keyfile,
    [     --with-client-keyfile=ARG      client host key file   [KEYFILE]],
    [
	if test "$iwthval"; then
	    CLIENT_HOST_KEY_FILE="$withval"
	else
	    AC_MSG_WARN([*** You must supply an argument to the --with-client-keyfile option.])
	fi
    ]
)
: ${CLIENT_HOST_KEY_FILE:=KEYFILE}
AC_DEFINE_UNQUOTED(CLIENT_HOST_KEY_FILE,$CLIENT_HOST_KEY_FILE)

AC_ARG_WITH(ticket-lifetime,
    [     --with-ticket-lifetime=ARG     ticket lifetime        [128]],
    [
	if test "$withval"; then
	    TICKET_LIFETIME="$withval"
	else
	    AC_MSG_WARN([*** You must supply an argument to the --with-ticket-lifetime option.])
	fi
    ]
)
: ${TICKET_LIFETIME:=128}
AC_DEFINE_UNQUOTED(TICKET_LIFETIME,$TICKET_LIFETIME)

AC_ARG_WITH(db,
    [   --with-db={db,dbm,gdbm,ndbm} use the given library for dbm_open],
    [
	if test "$withval"; then
	    DB_STYLE="$withval"
	else
	    AC_MSG_WARN([*** You must supply an argument to the --with-db option.])
	fi
    ]
)
if test "$DB_STYLE"; then
    case "$DB_STYLE" in
	db)	;;
	dbm)	;;
	gdbm)	;;
	ndbm)	;;
	*)
	    DB_STYLE=
	    AC_MSG_WARN([*** Unknown argument $withval given to --with-db.  Choose from db, dbm, gdbm, ndbm.])
	    ;;
    esac
fi
    
AC_ARG_WITH(mmap,
    [   --with-mmap            force use of mmap instead of shared memory support],
    FORCE_MMAP=yes
)
: ${FORCE_MMAP:=no}

AC_ARG_ENABLE(buffered-dump,
    [   --enable-buffered-dump buffer the dumping sockets on the server for speed],
    DUMPER_SOCKET_BUFFERING=yes
)
: ${DUMPER_SOCKET_BUFFERING:=no}
if test "$DUMPER_SOCKET_BUFFERING" = yes; then
    AC_DEFINE(DUMPER_SOCKET_BUFFERING)
fi    

AC_ARG_WITH(assertions,
    [   --with-assertions      compile assertions into code],
    ASSERTIONS=yes
)
: ${ASSERTIONS:=no}
if test "$ASSERTIONS" = yes; then
    AC_DEFINE(ASSERTIONS)
fi

AC_ARG_WITH(debugging,
    [   --with-debugging       record runtime debugging information into /tmp],
    DEBUGGING=yes
)
: ${DEBUGGING:=no}
if test "$DEBUGGING" = yes; then
    AC_DEFINE(DEBUG_CODE)
fi

AC_ARG_WITH(pid-debug-files,
    [   --with-pid-debug-files append pid's to program's /tmp debugging filenames],
    DEBUG_FILE_WITH_PID=yes
)
: ${DEBUG_FILE_WITH_PID:=no}
if test "$DEBUG_FILE_WITH_PID" = yes; then
    AC_DEFINE(DEBUG_FILE_WITH_PID)
fi

(
    test "x$prefix" = xNONE && prefix=$ac_default_prefix
    test "x$exec_prefix" = xNONE && exec_prefix=${prefix}

    tmp=`eval echo "$bindir"`
    AC_DEFINE_UNQUOTED(bindir,"$tmp")

    tmp=`eval echo "$libexecdir"`
    AC_DEFINE_UNQUOTED(libexecdir,"$tmp")

    tmp=`eval echo $mandir`
    AC_DEFINE_UNQUOTED(mandir,"$tmp")
)

AC_CHECK_PROG(CC, gcc, gcc)
if test -z "$CC"; then
  AC_CHECK_PROG(CC, cc, cc, , , /usr/ucb/cc)
  test -z "$CC" && AC_MSG_ERROR([no acceptable cc found in \$PATH])
fi

dnl Set the order of dump programs to look for.  Finding the proper file
dnl system dumping program is problematic.  Some systems, notably HP-UX
dnl and AIX, have both the backup and dump programs.  HP-UX can't use the
dnl the backup program while AIX systems can't use the dump program.  So
dnl a variable is set up here to specify the order of dump programs to
dnl search for on the system.
DUMP_PROGRAMS="ufsdump vdump dump backup"

dnl This specifies the flag for mt that tells mt the proper tape drive
dnl to use.
MT_FILE_FLAG="-f"
AC_SUBST(MT_FILE_FLAG)
case "$target" in
    *-dec-osf*)
			# setpgrp takes void
			ac_cv_func_setpgrp_void=yes
			AC_DEFINE(STATFS_OSF1)
			;;
    *-freebsd*)
			;;
    *-hp-*)
			MT_FILE_FLAG="-t"
			case "$CC" in
			    *gcc*)
				;;
			    *cc*)
				AMANDA_CFLAGS="-Ae $AMANDA_CFLAGS"
				;;
			esac
			;;
  *-ibm-aix*)
			DUMP_PROGRAMS="ufsdump vdump backup dump"
			AC_DEFINE(AIX_TAPEIO)
			AC_DEFINE(AIX_BACKUP)
			;;
  m88k-motorola-sysv4)
			;;
  *-nextstep3)
			;;
  *-pc-linux-*)
			;;
  *-sgi-irix3*)
			dnl The old cc won't work!
			CC=gcc
			;;
  *-sgi-irix4*)
			;;
  *-sgi-irix5*)
			;;
  *-sgi-irix6*)
			;;
  *-solaris2*)
			;;
  *-sun-sunos4.1*)
			;;
  sparc-unknown-linux-gnu)
			;;
  *-ultrix*)
			AC_DEFINE(STATFS_ULTRIX)
			AC_DEFINE(DUMP_RETURNS_1)
			;;
  *)
			cat <<END

*****
This machine, target type $target, is not known
to be fully supported by this configure script.  If the
installation of Amanda on this system succeeds or needed
any patches, please email amanda-hackers@cs.umd.edu with
the patches or an indication of the sucess or failure of
the Amanda installation on your system.
*****

END
		;;
esac

dnl Set the default CFLAGS to -g if CFLAGS has not been set.
if test "x" = "x$CFLAGS"; then
    CFLAGS=-g
fi
OLD_CFLAGS="$CFLAGS"
CFLAGS="$CFLAGS $AMANDA_CPPFLAGS $AMANDA_CFLAGS"
CPPFLAGS="$CPPFLAGS $AMANDA_CPPFLAGS"
LDFLAGS="$LDFLAGS $AMANDA_LDFLAGS"
AC_SUBST(AMANDA_CFLAGS)

dnl Check for programs.
AC_PATH_PROGS(AR,ar,,$LOCSYSPATH)

BUILD_AMPLOT_BIN="amplot"
BUILD_AMPLOT_LIBEXEC="amcat.awk amplot.awk amplot.g amplot.gp"
AC_SUBST(BUILD_AMPLOT_BIN)
AC_SUBST(BUILD_AMPLOT_LIBEXEC)

AC_PROG_AWK
AMANDA_PROG_AWK_VAR
if test "$amanda_cv_awk_var_assignment" = no; then
    BUILD_AMPLOT_BIN=
    BUILD_AMPLOT_LIBEXEC=
    AC_MSG_WARN([*** Your $awk cannot do command line variable assignment.  Amplot will not be installed.])
fi

AC_PROG_YACC
AC_PATH_PROGS(CAT,cat,,$LOCSYSPATH)
if test -z "$CAT"; then
    CAT=cat
fi
AC_PATH_PROGS(COMPRESS,compress,,$LOCSYSPATH)
AC_PATH_PROGS(DD,dd,,$LOCSYSPATH)
AC_PATH_PROGS(EGREP,egrep,,$LOCSYSPATH)

AC_PATH_PROGS(GNUPLOT,gnuplot,,$LOCSYSPATH)
if test -z "$GNUPLOT"; then
    BUILD_AMPLOT_BIN=
    BUILD_AMPLOT_LIBEXEC=
    AC_MSG_WARN([*** You do not have gnuplot.  Amplot will not be installed.])
fi

AC_PATH_PROGS(GREP,grep,,$LOCSYSPATH)
if test -z "$GREP"; then
    GREP=grep
fi
AC_DEFINE_UNQUOTED(GREP,"$GREP")

AC_PATH_PROGS(GNUTAR,gtar gnutar tar,,$LOCSYSPATH)
if test "$GNUTAR"; then
  case "`\"$GNUTAR\" --version 2>&1`" in
   *GNU*tar*)
		AC_DEFINE_UNQUOTED(GNUTAR,"$GNUTAR")
		;;
   *)
		AC_MSG_WARN([*** $GNUTAR is not GNU tar, so it will not be used.])
		GNUTAR=
		;;
  esac
fi

AC_PATH_PROGS(GZIP,gzip,,$LOCSYSPATH)
if test "$GZIP"; then
    AC_DEFINE(HAVE_GZIP)
    AC_DEFINE_UNQUOTED(COMPRESS_PATH,"$GZIP")
    AC_DEFINE_UNQUOTED(COMPRESS_SUFFIX,".gz")
    AC_DEFINE_UNQUOTED(COMPRESS_FAST_OPT,"--fast")
    AC_DEFINE_UNQUOTED(COMPRESS_BEST_OPT,"--best")
    AC_DEFINE_UNQUOTED(UNCOMPRESS_PATH,"$GZIP")
    AC_DEFINE_UNQUOTED(UNCOMPRESS_OPT,"-dc")
else
    if test "$COMPRESS"; then
	AC_DEFINE_UNQUOTED(COMPRESS_PATH,"$COMPRESS")
	AC_DEFINE_UNQUOTED(COMPRESS_SUFFIX,".Z")
	AC_DEFINE_UNQUOTED(COMPRESS_FAST_OPT,"-f")
	AC_DEFINE_UNQUOTED(COMPRESS_BEST_OPT,"-f")
	AC_DEFINE_UNQUOTED(UNCOMPRESS_PATH,"$COMPRESS")
	AC_DEFINE_UNQUOTED(UNCOMPRESS_OPT,"-dc")
    else
	dnl If we have to use cat, we don't define COMPRESS_FAST_OPT,
	dnl COMPRESS_BEST_OPT, or UNCOMPRESS_OPT as "" since cat will look
	dnl look for a file by the name of "".
	AC_MSG_WARN([*** Cannot find either gzip or compress.  Using cat. ***])
	AC_DEFINE_UNQUOTED(COMPRESS_PATH,"$CAT")
	AC_DEFINE_UNQUOTED(COMPRESS_SUFFIX,"")
	AC_DEFINE_UNQUOTED(UNCOMPRESS_PATH,"$COMPRESS")
    fi
fi

AC_PATH_PROGS(MAILER,Mail mailx)
if test "$MAILER"; then
    AC_DEFINE_UNQUOTED(MAILER,"$MAILER")
fi

AC_PATH_PROGS(MT,mt,,$LOCSYSPATH)

AC_PATH_PROGS(MTX,mtx,,$LOCSYSPATH)
if test "$MTX"; then
    MTX=mtx
fi

AC_PATH_PROGS(PCAT,pcat,,$LOCSYSPATH)
AC_PATH_PROGS(PERL,perl5 perl,,$LOCSYSPATH)

AC_PROG_RANLIB

dnl AC_PATH_PROGS(MAKEINFO,makeinfo,,$LOCSYSPATH)
dnl AC_PATH_PROGS(TEXI2DVI,texi2dvi,,$LOCSYSPATH)
SHELL=
AC_PATH_PROGS(SHELL,sh,,$LOCSYSPATH)

AC_PATH_PROGS(RSH,remsh,,$SYSPATH)
if test "$RSH"; then
    AC_DEFINE_UNQUOTED(RSH_COMMAND,"$RSH")
else
    AC_PATH_PROGS(RSH,rsh,,$SYSPATH)
    if test "$RSH"; then
	AC_DEFINE_UNQUOTED(RSH_COMMAND,"$RSH")
    fi
fi

AC_PATH_PROGS(DUMP,$DUMP_PROGRAMS,,$SYSLOCPATH)
if test "$DUMP"; then
    AC_DEFINE_UNQUOTED(DUMP,"$DUMP")
fi
case "$DUMP" in
    *vdump*)
	AC_DEFINE(OSF1_VDUMP)
	AC_DEFINE(USE_RUNDUMP)
	;;
esac

AC_PATH_PROGS(RESTORE,ufsrestore vrestore restore,,$SYSLOCPATH)
if test "$RESTORE"; then
    AC_DEFINE_UNQUOTED(RESTORE,"$RESTORE")
fi

AC_PATH_PROGS(XFSDUMP,xfsdump,,$SYSLOCPATH)
if test "$XFSDUMP"; then
    AC_DEFINE_UNQUOTED(XFSDUMP,"$XFSDUMP")
    AC_DEFINE(USE_RUNDUMP)
fi
AC_PATH_PROGS(XFSRESTORE,xfsrestore,,$SYSLOCPATH)
if test "$XFSDUMP"; then
    AC_DEFINE_UNQUOTED(XFSRESTORE,"$XFSRESTORE")
fi

dnl Handle all of the substitutions to make amplot work.
if test "$PCAT"; then
    AMPLOT_CAT_PACK="if(o==\"z\")print \"$PCAT\"; else"
else
    AMPLOT_CAT_PACK=
fi
if test "$COMPRESS"; then
    AMPLOT_COMPRESS=$COMPRESS
    AMPLOT_CAT_COMPRESS="if(o==\"Z\")print \"$COMPRESS -dc\"; else"
else
    AMPLOT_CAT_COMPRESS=
fi
if test "$GZIP"; then
    AMPLOT_COMPRESS=$GZIP
    AMPLOT_CAT_GZIP="if(o==\"gz\")print \"$GZIP -dc\"; else"
else
    AMPLOT_CAT_GZIP=
fi
AC_SUBST(AMPLOT_COMPRESS)
AC_SUBST(AMPLOT_CAT_GZIP)
AC_SUBST(AMPLOT_CAT_COMPRESS)
AC_SUBST(AMPLOT_CAT_PACK)

dnl Empty GZIP so that make dist works.
GZIP=

AC_MSG_CHECKING(for rewinding tape device)
dnl Check for the /dev/rmt directory and use what's in there.  Otherwise
dnl look for tape devices in /dev.  For the devices in /dev/rmt, we want
dnl to use the Berkeley behavior of reading the first record of the next
dnl tape file after 0 bytes are returned upon reading to the next tape
dnl mark, instead of returning an error.  Look for devices that have a
dnl 'b' in their name.
tape_dev=/dev/null
nr_tape_dev=/dev/null
if test -d /dev/rmt; then
    dnl See if we can find two devices, one being the norewind version
    dnl of the other.  Devices in this directory are normally a digit
    dnl followed by some characters.  We also want the Berkely behavior,
    dnl since Amanda needs it for amrestore.
    for num in 9 8 7 6 5 4 3 2 1 0; do
	td=/dev/rmt/${num}b
	ntd=/dev/rmt/${num}bn
	if test -r $td && test -r $ntd; then
	    tape_dev=$td
	    nr_tape_dev=$ntd
	fi
    done
else
    dnl Look for tape devices in /dev.
    for num in 9 8 7 6 5 4 3 2 1 0; do
	td=/dev/rst${num}
	ntd=/dev/nrst${num}
	if test -r $td && test -r $ntd; then
	    tape_dev=$td
	    nr_tape_dev=$ntd
	fi
    done
fi

if test "$TAPE_REWIND_DEVICE" = /dev/null; then
    TAPE_REWIND_DEVICE=$tape_dev
fi

if test "$TAPE_NO_REWIND_DEVICE" = /dev/null; then
    TAPE_NO_REWIND_DEVICE=$nr_tape_dev
fi

AC_MSG_RESULT($TAPE_REWIND_DEVICE)

AC_MSG_CHECKING(for no rewinding tape device)
AC_MSG_RESULT($TAPE_NO_REWIND_DEVICE)

dnl Check for a good directory where index files are held before being
dnl to the tape host.
AC_CACHE_CHECK(
    [for index files temporary holding directory],
    amanda_cv_index_tmp_dir,
    [
	if test -d /var/tmp; then
	    amanda_cv_index_tmp_dir=/var/tmp
	elif test -d /usr/tmp; then
	    amanda_cv_index_tmp_dir=/usr/tmp
	else
	    amanda_cv_index_tmp_dir=/tmp
	fi
    ]
)
AC_DEFINE_UNQUOTED(INDEX_TMP_DIR,"$amanda_cv_index_tmp_dir")

dnl Checks for compilers, typedefs, structures, and compiler characteristics.
AC_PROG_CC
AC_PROG_GCC_TRADITIONAL
AC_C_CONST
AMANDA_C_VOLATILE
AC_TYPE_OFF_T
AC_TYPE_PID_T
AC_TYPE_SIZE_T
AC_TYPE_UID_T
AC_TYPE_SIGNAL
AC_STRUCT_TM
AC_STRUCT_ST_BLOCKS
AC_PROG_LEX
AC_DECL_YYTEXT

dnl Checks for header files.
AC_HEADER_STDC
AC_HEADER_DIRENT
AC_HEADER_SYS_WAIT
AC_HEADER_TIME
AC_CHECK_HEADERS(\
	asm/byteorder.h \
	chio.h \
	db.h \
	dbm.h \
	fcntl.h \
	fstab.h \
	gdbm.h \
	grp.h \
	history.h \
	mntent.h \
	ndbm.h \
	netdb.h \
	readline.h \
	readline/history.h \
	readline/readline.h \
	stdlib.h \
	string.h \
	sys/fcntl.h \
	sys/file.h \
	sys/ioctl.h \
	sys/ipc.h \
	sys/mman.h \
	sys/mntent.h \
	sys/mount.h \
	sys/param.h \
	sys/select.h \
	sys/shm.h \
	sys/stat.h \
	sys/statfs.h \
	sys/statvfs.h \
	sys/time.h \
	sys/types.h \
	sys/vfs.h \
	sys/vfstab.h \
	syslog.h \
	unistd.h)

dnl Do not build seagate-changer if we can't find chio.h.
if test "$ac_cv_header_chio_h" = no; then
    BUILD_CHANGER_PROGS_LIBEXEC=
fi

dnl cur_colr is on some HP's
AC_CHECK_LIB(cur_colr,main)

AC_CHECK_LIB(intl,main)

dnl Make sure we don't use -lnsl and -lsun on Irix systems.
case "$target" in
    *sgi-irix*)
			AC_CHECK_LIB(socket,main)
			;;
    *)
			AC_CHECK_LIB(nsl,main)
			AC_CHECK_LIB(socket,main)
			AC_CHECK_LIB(sun,main)
			;;
esac

AC_CHECK_LIB(termcap,main)
if test "$ac_cv_lib_termcap_main" = yes; then
    AC_CHECK_LIB(readline,main)
    if test "$ac_cv_lib_readline_main" != yes; then
	AC_MSG_WARN([*** libreadline not found! No history and command line editing in amrecover!])
    fi
else
    AC_MSG_WARN([*** libtermcap not found! No history and command line editing in amrecover!])
fi
dnl

dnl Check if the system does support the user requested database library.
dnl Begin by checking for the header file.  If it is not there, then do
dnl not use the library.  If the header file is there, then try to link
dnl against the library.   If it's not there, then try to link using
dnl just the -lc library.  If the link against -lc fails, then do not
dnl use this library.
DB_HEADER=
DB_LIB=

dnl Testing if libc contains the dbm_open routine is tested for a
dnl lot of times below.  We do it once here now.
save_LIBS="$LIBS"
AC_CHECK_LIB(c,dbm_open)
LIBS="$save_LIBS"

case "$DB_STYLE" in
    db)
	if test "$ac_cv_header_db_h" = yes; then
	    AC_CHECK_LIB(db,main)
	    if test "$ac_cv_lib_db_main" = yes; then
		AC_CHECK_LIB(db,dbm_open)
		if test "$ac_cv_lib_db_dbm_open" = yes; then
		    DB_HEADER=db.h
		    DB_LIB=db
		else
		    DB_STYLE=
		    AC_MSG_WARN([*** db database library requested but dbm_open not found in -ldb.])
		fi
	    else
		if test "$ac_cv_lib_c_dbm_open" = yes; then
		    DB_HEADER=db.h
		    DB_LIB=c
		else
		    DB_STYLE=
		    AC_MSG_WARN([*** db library requested but -ldb doesn't exist and dbm_open cannot be found.])
		fi
	    fi
	else
	    DB_STYLE=
	    AC_MSG_WARN([*** db batabase library requested but db.h not found.])
	fi
	;;

    dbm)
	if test "$ac_cv_header_dbm_h" = yes; then
	    AC_CHECK_LIB(dbm,main)
	    if test "$ac_cv_lib_dbm_main" = yes; then
		AC_CHECK_LIB(dbm,dbm_open)
		if test "$ac_cv_lib_dbm_dbm_open" = yes; then
		    DB_HEADER=dbm.h
		    DB_LIB=dbm
		else
		    DB_STYLE=
		    AC_MSG_WARN([*** dbm database library requested but dbm_open not found in -ldbm.])
		fi
	    else
		if test "$ac_cv_lib_c_dbm_open" = yes; then
		    DB_HEADER=dbm.h
		    DB_LIB=c
		else
		    DB_STYLE=
		    AC_MSG_WARN([*** dbm library requested but -ldbm doesn't exist and dbm_open cannot be found.])
		fi
	    fi
	else
	    DB_STYLE=
	    AC_MSG_WARN([*** dbm database library requested but dbm.h not found.])
	fi
	;;

    gdbm)
	if test "$ac_cv_header_gdbm_h" = yes; then
	    AC_CHECK_LIB(gdbm,main)
	    if test "$ac_cv_lib_gdbm_main" = yes; then
		AC_CHECK_LIB(gdbm,dbm_open)
		if test "$ac_cv_lib_gdbm_dbm_open" = yes; then
		    DB_HEADER=gdbm.h
		    DB_LIB=gdbm
		else
		    DB_STYLE=
		    AC_MSG_WARN([*** gdbm database library requested but -lgdm not found.])
		fi
	    else
		if test "$ac_cv_lib_c_dbm_open" = yes; then
		    DB_HEADER=gdbm.h
		    DB_LIB=c
		else
		    DB_STYLE=
		    AC_MSG_WARN([*** gdbm library requested but -lgdm doesn't exist and dbm_open cannot be found.])
		fi
	    fi
	else
	    DB_STYLE=
	    AC_MSG_WARN([*** gdbm database library requested but gdbm.h not found.])
	fi
	;;

    ndbm)
	if test "$ac_cv_header_ndbm_h" = yes; then
	    AC_CHECK_LIB(ndbm,main)
	    if test "$ac_cv_lib_ndbm_main" = yes; then
		AC_CHECK_LIB(ndbm,dbm_open)
		if test "$ac_cv_lib_ndbm_dbm_open" = yes; then
		    DB_HEADER=ndbm.h
		    DB_LIB=ndbm
		else
		    DB_STYLE=
		    AC_MSG_WARN([*** ndbm database library requested but -lgdm not found.])
		fi
	    else
		if test "$ac_cv_lib_c_dbm_open" = yes; then
		    DB_HEADER=ndbm.h
		    DB_LIB=c
		else
		    DB_STYLE=
		    AC_MSG_WARN([*** ndbm library requested but -lndbm doesn't exist and dbm_open cannot be found.])
		fi
	    fi
	else
	    DB_STYLE=
	    AC_MSG_WARN([*** ndbm database library requested but ndbm.h not found.])
	fi
	;;
esac

if test -z "$DB_STYLE"; then
    AC_CHECK_LIB(ndbm,dbm_open)
    if test "$ac_cv_lib_ndbm_dbm_open" = yes && test "$ac_cv_header_ndbm_h" = yes; then
	DB_STYLE=ndbm
	DB_HEADER=ndbm.h
	DB_LIB=ndbm
    elif test "$ac_cv_lib_c_dbm_open" = yes &&  test "$ac_cv_header_ndbm_h" = yes; then
	DB_STYLE=ndbm
	DB_HEADER=ndbm.h
	DB_LIB=c
    else
	AC_CHECK_LIB(db,dbm_open)
	if test "$ac_cv_lib_db_dbm_open" = yes && test "$ac_cv_header_db_h" = yes; then
	    DB_STYLE=db
	    DB_HEADER=db.h
	    DB_LIB=db
	elif test "$ac_cv_lib_c_dbm_open" = yes && test "$ac_cv_header_db_h" = yes; then
	    DB_STYLE=db
	    DB_HEADER=db.h
	    DB_LIB=c
	else
	    AC_CHECK_LIB(dbm,dbm_open)
	    if test "$ac_cv_lib_db_dbm_open" = yes && test "$ac_cv_header_dbm_h" = yes; then
		DB_STYLE=dbm
		DB_HEADER=dbm.h
		DB_LIB=dbm
	    elif test "$ac_cv_lib_c_dbm_open" = yes && test "$ac_cv_header_dbm_h" = yes; then
		DB_STYLE=dbm
		DB_HEADER=dbm.h
		DB_LIB=c
	    else
		AC_CHECK_LIB(gdbm,dbm_open)
		if test "$ac_cv_lib_gdbm_dbm_open" = yes && test "$ac_cv_header_gdbm_h" = yes; then
		    DB_STYLE=gdbm
		    DB_HEADER=gdbm.h
		    DB_LIB=gdbm
		elif test "$ac_cv_lib_c_dbm_open" = yes && test "$ac_cv_header_gdbm_h" = yes; then
		    DB_STYLE=gdbm
		    DB_HEADER=gdbm.h
		    DB_LIB=c
		else
		    if test "$ac_cv_lib_c_dbm_open" = yes; then
			AC_MSG_WARN([*** A dbm_open() routine was found in -lc, but no header])
			AC_MSG_WARN([*** files exist.  Please remedy the situation.  You may])
			AC_MSG_WARN([*** want to install the gdbm library from])
		    else
			AC_MSG_WARN([*** No dbm_open() routine found!  Please download and install])
		    fi
		    AC_MSG_WARN([*** ftp://prep.ai.mit.edu/pub/gnu/gdbm-1.7.3.tar.gz.])
		    AC_MSG_WARN([*** This system will not support the Amanda server.])
		    NO_SERVER_MODE=true
		fi
	    fi
	fi
    fi
fi
if test "$DB_STYLE"; then
    AC_MSG_CHECKING([for database])
    AC_MSG_RESULT([header is $DB_HEADER, linking against -l$DB_LIB])
    case "$DB_STYLE" in
	db)	AC_DEFINE(USE_DB_H)   ;;
	dbm)	AC_DEFINE(USE_DBM_H)  ;;
	gdbm)	AC_DEFINE(USE_GDBM_H) ;;
	ndbm)	AC_DEFINE(USE_NDBM_H) ;;
    esac
fi

AC_CACHE_CHECK(
    [for struct datum declared in header files],
    amanda_cv_struct_datum,
    [
	AC_TRY_COMPILE(
	    [
		#if defined(USE_DB_H)
		#  include <db.h>
		#elif defined(USE_DBM_H)
		#  include <dbm.h>
		#elif defined(USE_GDBM_H)
		#  include <gdbm.h>
		#elif defined(USE_NDBM_H)
		#  include <ndbm.h>
		#endif
	    ],
	    [
		datum a;
	    ],
	    amanda_cv_struct_datum=yes,
	    amanda_cv_struct_datum=no
	)
    ]
    if test "$amanda_cv_struct_datum" = yes; then
	AC_DEFINE(HAVE_STRUCT_DATUM)
    fi
)

dnl Checks for library functions and if the function is declared in
dnl an appropriate header file.  Add some of the missing functions
dnl to LIBOBJS.
ICE_CHECK_DECL(accept,sys/types.h sys/socket.h)
AC_REPLACE_FUNCS(add_exclude)
AC_FUNC_ALLOCA
ICE_CHECK_DECL(atof,stdlib.h)
ICE_CHECK_DECL(bind,sys/types.h sys/socket.h)
ICE_CHECK_DECL(bzero,string.h)
AC_REPLACE_FUNCS(check_exclude)
ICE_CHECK_DECL(closelog,syslog.h)
ICE_CHECK_DECL(connect,sys/types.h sys/socket.h)
ICE_CHECK_DECL(dbm_open,${DB_HEADER-no/db/header/file})
ICE_CHECK_DECL(fclose,stdio.h)
ICE_CHECK_DECL(fflush,stdio.h)
ICE_CHECK_DECL(flock,sys/file.h)
ICE_CHECK_DECL(fprintf,stdio.h)
ICE_CHECK_DECL(fputc,stdio.h)
ICE_CHECK_DECL(fputs,stdio.h)
ICE_CHECK_DECL(fread,stdio.h)
ICE_CHECK_DECL(fwrite,stdio.h)
AC_REPLACE_FUNCS(getcwd)
AC_CHECK_FUNCS(getfsent)
ICE_CHECK_DECL(gethostname,unistd.h)
AC_FUNC_GETMNTENT
ICE_CHECK_DECL(getopt,stdlib.h unistd.h)
ICE_CHECK_DECL(getpeername,sys/types.h sys/socket.h)
ICE_CHECK_DECL(getsockname,sys/types.h sys/socket.h)
ICE_CHECK_DECL(getsockopt,sys/types.h sys/socket.h)
ICE_CHECK_DECL(gettimeofday,time.h sys/time.h)
AMANDA_FUNC_GETTIMEOFDAY_ARGS
AC_CHECK_FUNCS(getvfsent isascii)
ICE_CHECK_DECL(initgroups,grp.h sys/types.h unistd.h)
ICE_CHECK_DECL(listen,sys/types.h sys/socket.h)
ICE_CHECK_DECL(lstat,sys/types.h sys/stat.h)
ICE_CHECK_DECL(memset,string.h)
AC_CHECK_FUNCS(mkdir)
ICE_CHECK_DECL(mktemp,stdlib.h)
AC_REPLACE_FUNCS(mktime)
ICE_CHECK_DECL(mktime,time.h sys/time.h)
AC_FUNC_MMAP
ICE_CHECK_DECL(openlog,syslog.h)
ICE_CHECK_DECL(pclose,stdio.h)
ICE_CHECK_DECL(perror,stdio.h)
ICE_CHECK_DECL(printf,stdio.h)
ICE_CHECK_DECL(puts,stdio.h)
ICE_CHECK_DECL(recvfrom,sys/types sys/socket.h)
ICE_CHECK_DECL(remove,stdio.h)
ICE_CHECK_DECL(rename,stdio.h)
ICE_CHECK_DECL(rewind,stdio.h)
AC_CHECK_FUNCS(rmdir)
ICE_CHECK_DECL(ruserok,netdb.h sys/socket.h)
ICE_CHECK_DECL(select,sys/types.h sys/socket.h sys/select.h time.h sys/time.h)
AMANDA_FUNC_SELECT_ARG_TYPE
ICE_CHECK_DECL(sendto,sys/types.h sys/socket.h)

dnl arguments for setpgrp or not
AC_CHECK_FUNC(setpgrp,[AC_FUNC_SETPGRP])
ICE_CHECK_DECL(setpgrp,sys/types.h unistd.h)

ICE_CHECK_DECL(setsockopt,sys/types.h sys/socket.h)

AC_CHECK_FUNCS(shmget,
    [
	AMANDA_FUNC_SHM_ARG_TYPE
	if test "$FORCE_MMAP" = no; then
	    AC_DEFINE(HAVE_SYSVSHM)
	fi
    ]
)
ICE_CHECK_DECL(shmat,sys/types.h sys/ipc.h sys/shm.h)
ICE_CHECK_DECL(shmctl,sys/types.h sys/ipc.h sys/shm.h)
ICE_CHECK_DECL(shmdt,sys/types.h sys/ipc.h sys/shm.h)
ICE_CHECK_DECL(shmget,sys/types.h sys/ipc.h sys/shm.h)

if test "$ac_cv_func_mmap_fixed_mapped" != yes; then
    if test "$FORCE_MMAP" = yes; then
	AC_MSG_WARN([*** --with-mmap used on a system with no mmap() support!])
	AC_MSG_WARN([*** This system will not support the Amanda server.])
	NO_SERVER_MODE=true
    else
	if test "$ac_cv_func_shmget" != yes; then
	    AC_MSG_WARN([*** Neither shmget() nor mmap() found!])
	    AC_MSG_WARN([*** This system will not support the Amanda server.])
	    NO_SERVER_MODE=true
	fi
    fi
fi

AC_CHECK_FUNCS(sigaction sigemptyset sigvec)
ICE_CHECK_DECL(socket,sys/types.h sys/socket.h)
ICE_CHECK_DECL(socketpair,sys/types.h sys/socket.h)
ICE_CHECK_DECL(sscanf,stdio.h)
AC_CHECK_FUNCS(statfs statvfs)
AC_REPLACE_FUNCS(strdup)
AC_REPLACE_FUNCS(strerror)
ICE_CHECK_DECL(strerror,string.h)
AC_FUNC_STRFTIME
ICE_CHECK_DECL(strftime,time.h sys/time.h)
ICE_CHECK_DECL(strncasecmp,string.h)
AC_REPLACE_FUNCS(strstr)
ICE_CHECK_DECL(syslog,syslog.h)
ICE_CHECK_DECL(system,stdlib.h)
ICE_CHECK_DECL(time,time.h sys/time.h)
ICE_CHECK_DECL(tolower,ctype.h)
ICE_CHECK_DECL(toupper,ctype.h)
ICE_CHECK_DECL(ungetc,stdio.h)
AC_FUNC_VPRINTF
ICE_CHECK_DECL(vfprintf,stdio.h)
ICE_CHECK_DECL(vsprintf,stdio.h)

dnl disk device prefixes
AC_MSG_CHECKING(disk device prefixes)
dnl Use df to find the mount point for the root filesystem.  Use
dnl the positional parameters to find the particular line from df
dnl that contains the root paritition.  We put it in a subshell so
dnl that the original positional parameters are not messed with.
dfline=`(
    df | while read line; do
	set -- $line
	while test $# -gt 0; do
	    if test "$1" = "/"; then
		echo $line
		break 2
	    fi
	    shift
	done
    done
) | sed 's/(//' | sed 's/)//' `

dnl Search for the mount point by using expr to find the parameter
dnl with dev in it.
mount=`(
    set -- $dfline
    while test $# -gt 0; do
	if expr "$1" : '.*dev' >/dev/null 2>&1; then
	    echo $1
	    break
	fi
	shift
    done
)`

FIXDEVS_MESSAGE=
case `dirname $mount` in
    /dev/dsk)
		dev_prefix=/dev/dsk/
		;;
    /dev/vg*)
		if test -d /dev/dsk; then
		    dev_prefix=/dev/dsk/
		else
		    dev_prefix=/dev/
		fi
		FIXDEVS_MESSAGE="*** Run amhpfixdevs on HP-UX systems using /dev/vg??."
		;;
	*)
		# Match everything else, including an empty $mount.
		if test -d /dev/dsk; then
		    dev_prefix=/dev/dsk/
		elif test -d /dev; then
		    dev_prefix=/dev/
		else
		    dev_prefix=/
		fi
		;;
esac

if test "$dev_prefix" = /dev/dsk/; then
    if test -d /dev/rdsk; then
	rdev_prefix=/dev/rdsk/
    else
	rdev_prefix=/dev/dsk/
    fi
else
    # Some systems, notably Linux, do not have raw disk devices
    # names.  Check this by trying to see if a raw disk device name
    # exists using the normal raw device path prepended to the
    # mount point of the root filesystem.
    if test "$mount"; then
	dev_name="/dev/r`basename $mount`"
	if test -b $dev_name || test -c $dev_name; then
	    rdev_prefix=/dev/r
	else
	    rdev_prefix=/dev/
	fi
    else
	rdev_prefix=$dev_prefix
    fi
fi

if test "$DEV_PREFIX" && test "$RDEV_PREFIX"; then
    AC_MSG_RESULT((predefined) $DEV_PREFIX - $RDEV_PREFIX)
else
    DEV_PREFIX=$dev_prefix
    RDEV_PREFIX=$rdev_prefix
    AC_MSG_RESULT($DEV_PREFIX - $RDEV_PREFIX)
fi
AC_DEFINE_UNQUOTED(DEV_PREFIX,"${DEV_PREFIX}")
AC_DEFINE_UNQUOTED(RDEV_PREFIX,"${RDEV_PREFIX}")
if test "$FIXDEVS_MESSAGE"; then
    AC_MSG_WARN($FIXDEVS_MESSAGE)
fi

dnl maximum file size on holding disk
AC_CACHE_CHECK(
    [maximum file size],
    amanda_cv_max_file_size,
    [
	amanda_cv_max_file_size=0
	rm -f conftest.$ac_ext
cat <<EOF >conftest.$ac_ext
#include "confdefs.h"
#include <stdio.h>
#include <limits.h>
#ifdef HAVE_SYS_TYPES_H
# include <sys/types.h>
#endif
#include <sys/uio.h>

int main() { struct iovec test; unsigned long compare = 0, last = 0;
test.iov_len = 1; while((test.iov_len > 0) && (test.iov_len > last)) {
if ((last = test.iov_len) > compare) compare = test.iov_len;
test.iov_len = (test.iov_len << 1) | 0x01; }
printf("%ld\n",compare/1024); return 0; }

EOF
	${CC-cc} -o conftest $CFLAGS $CPPFLAGS $LDFLAGS conftest.$ac_ext $LIBS >/dev/null 2>/dev/null
	if test $? = 0; then
	    MAXFILESIZE=`./conftest`
	    STATUS=$?
	    if test "$STATUS" = 0; then
		amanda_cv_max_file_size=$MAXFILESIZE
	    fi
	fi
	rm -f conftest* /tmp/conftest.lock
    ]
)
if test "$amanda_cv_max_file_size" -gt 0; then
    AC_DEFINE_UNQUOTED(MAXFILESIZE,$amanda_cv_max_file_size)
fi
dnl		AC_MSG_RESULT($amanda_cv_max_file_size kB)
dnl	else
dnl		AC_MSG_RESULT(undefined ($STATUS))
dnl	fi
dnl else
dnl	AC_MSG_RESULT(undefined (compiler error))
dnl fi

dnl lock/flock/fcntl
AC_CACHE_CHECK(
   [whether posix fcntl flock works],
   amanda_cv_posix_filelocking,
   [
rm -f conftest.$ac_ext
cat <<EOF >conftest.$ac_ext
#include "confdefs.h"
#include <stdio.h>
#ifdef HAVE_UNISTD_H
# include <unistd.h>
#endif
#ifdef HAVE_SYS_TYPES_H
# include <sys/types.h>
#endif
#ifdef HAVE_SYS_FILE_H
# include <sys/file.h>
#endif
#ifdef HAVE_SYS_STAT_H
# include <sys/stat.h>
#endif
#ifdef HAVE_FCNTL_H
# include <fcntl.h>
#endif
#ifdef HAVE_SYS_FCNTL_H
# include <sys/fcntl.h>
#endif

static struct flock lock = { F_UNLCK, SEEK_SET, 0, 0 };

main() { int lockfd; FILE *fp; if (lockfd =
open("/tmp/conftest.lock",O_RDWR|O_CREAT,0666) == -1) return(-1);
close(lockfd); chmod("/tmp/conftest.lock",0666); if (!(fp =
fopen("/tmp/conftest.lock", "r"))) return(-2); lock.l_type = F_RDLCK;
if (fcntl(fileno(fp), F_SETLKW, &lock) == -1) exit(1); lock.l_type =
F_UNLCK; if (fcntl(fileno(fp), F_SETLK, &lock) == -1) exit(2);
fclose(fp); if (!(fp = fopen("/tmp/conftest.lock", "w"))) return(-3);
lock.l_type = F_WRLCK; if (fcntl(fileno(fp), F_SETLKW, &lock) == -1)
exit(3); lock.l_type = F_UNLCK; if (fcntl(fileno(fp), F_SETLK, &lock)
== -1) exit(4); exit(0); }

EOF
${CC-cc} -o conftest $CFLAGS $CPPFLAGS $LDFLAGS conftest.$ac_ext $LIBS >/dev/null 2>/dev/null
if test $? = 0; then
    ./conftest
    STATUS=$?
    if test "$STATUS" = 0; then
	amanda_cv_posix_filelocking=yes
    else
	amanda_cv_posix_filelocking="no $(STATUS)"
    fi
else
    amanda_cv_posix_filelocking="no (compiler error)"
fi
rm -f conftest* /tmp/conftest.lock
])
if test "$amanda_cv_posix_filelocking" = yes; then
    AC_DEFINE(NEED_POSIX_FLOCK)
    HAS_RUNNING_FLOCK=1
fi

if test -z "$HAS_RUNNING_FLOCK"; then
AC_MSG_CHECKING(whether flock works)
rm -f conftest.$ac_ext
cat <<EOF >conftest.$ac_ext
#include "confdefs.h"
#include <stdio.h>
#ifdef HAVE_SYS_TYPES_H
# include <sys/types.h>
#endif
#ifdef HAVE_SYS_FILE_H
# include <sys/file.h>
#endif
#ifdef HAVE_SYS_STAT_H
# include <sys/stat.h>
#endif
#ifdef HAVE_FCNTL_H
# include <fcntl.h>
#endif
#ifdef HAVE_SYS_FCNTL_H
# include <sys/fcntl.h>
#endif

main() { 
#if defined(LOCK_EX) && defined(LOCK_UN)
int lockfd; FILE *fp; if (lockfd =
open("/tmp/conftest.lock",O_RDWR|O_CREAT,0666) == -1) return(-1);
close(lockfd); chmod("/tmp/conftest.lock",0666); if (!(fp =
fopen("/tmp/conftest.lock", "r"))) return(-2); if (flock(fileno(fp),
LOCK_EX) == -1) exit(1); if (flock(fileno(fp), LOCK_UN) == -1) exit(2);
fclose(fp); if (!(fp = fopen("/tmp/conftest.lock", "w"))) return(-3);
if (flock(fileno(fp), LOCK_EX) == -1) exit(3); if (flock(fileno(fp),
LOCK_UN) == -1) exit(4); exit(0);
#else
	exit(42);
#endif
}
EOF
${CC-cc} -o conftest $CFLAGS $CPPFLAGS $LDFLAGS conftest.$ac_ext $LIBS >/dev/null 2>/dev/null
if test $? = 0; then
    ./conftest
    STATUS=$?
    if test "$STATUS" = 0; then
	AC_MSG_RESULT(yes)
	HAS_RUNNING_FLOCK=1
    else
	AC_MSG_RESULT(no ($STATUS))
    fi
else
    AC_MSG_RESULT(no (compiler error))
fi
dnl cp conftest.c flocktest.c
rm -f conftest* /tmp/conftest.lock
fi

if test -z "$HAS_RUNNING_FLOCK"; then
AC_MSG_CHECKING(whether an lockf flock replacement works)
rm -f conftest.$ac_ext
cat <<EOF >conftest.$ac_ext
#include "confdefs.h"
#include <stdio.h>
#ifdef HAVE_SYS_TYPES_H
# include <sys/types.h>
#endif
#ifdef HAVE_SYS_FILE_H
# include <sys/file.h>
#endif
#ifdef HAVE_SYS_STAT_H
# include <sys/stat.h>
#endif
#ifdef HAVE_FCNTL_H
# include <fcntl.h>
#endif
#ifdef HAVE_SYS_FCNTL_H
# include <sys/fcntl.h>
#endif

/* consts from BSDish <sys/file.h> */
#ifndef LOCK_EX
#  define LOCK_EX 2
#  define LOCK_UN 8
#endif
 
/* sgi irix reportedly has F_ULOCK instead of F_UNLOCK */
#ifdef F_UNLOCK
#  define OUR_UNLOCK F_UNLOCK
#else
#  ifdef F_ULOCK
#    define OUR_UNLOCK F_ULOCK
#  else
     !!! error neither F_ULOCK or F_UNLOCK defined: cannot deal with this
#  endif
#endif

int flock(fd, operation)
int fd, operation;
{
    int prevpos;
 
    assert(operation == LOCK_EX || operation == LOCK_UN);
 
    /* save our current position */
    if((prevpos = lseek(fd, SEEK_CUR, 0)) == -1) return -1;
 
    /* a lock on the first byte of the file serves as our advisory file lock */
    if(lseek(fd, SEEK_SET, 0) == -1) return -1;
    if(operation == LOCK_EX) {
	if(lockf(fd, F_LOCK, 1) == -1) return -1;
    }
    else {
	if(lockf(fd, OUR_UNLOCK, 1) == -1) return -1;
    }
 
    /* restore our current position */
    if(lseek(fd, SEEK_SET, prevpos) == -1) return -1;
    return 0;
}

main() { 
int lockfd; FILE *fp; if (lockfd =
open("/tmp/conftest.lock",O_RDWR|O_CREAT,0666) == -1) return(-1);
close(lockfd); chmod("/tmp/conftest.lock",0666); if (!(fp =
fopen("/tmp/conftest.lock", "r"))) return(-2); if (flock(fileno(fp),
LOCK_EX) == -1) exit(1); if (flock(fileno(fp), LOCK_UN) == -1) exit(2);
fclose(fp); if (!(fp = fopen("/tmp/conftest.lock", "w"))) return(-3);
if (flock(fileno(fp), LOCK_EX) == -1) exit(3); if (flock(fileno(fp),
LOCK_UN) == -1) exit(4); exit(0);
}
EOF
${CC-cc} -o conftest $CFLAGS $CPPFLAGS $LDFLAGS conftest.$ac_ext $LIBS >/dev/null 2>/dev/null
if test $? = 0; then
    ./conftest
    STATUS=$?
    if test "$STATUS" = 0; then
	AC_MSG_RESULT(yes)
	HAS_RUNNING_FLOCK=1
	LIBOBJS="$LIBOBJS flock.o"
    else
	AC_MSG_RESULT(no ($STATUS))
    fi
else
    AC_MSG_RESULT(no (compiler error))
fi
dnl cp conftest.c flocktest.c
rm -f conftest* /tmp/conftest.lock
fi

if test -z "$HAS_RUNNING_FLOCK"; then
    AC_MSG_WARN([*** No working flock/fcntl capability found!])
    AC_MSG_WARN([*** This system will not support the Amanda server.])
fi

dnl restore the old CFLAGS
CFLAGS="$OLD_CFLAGS"

dnl extra substitution parameters
AC_SUBST(MTX)
AC_SUBST(ac_n)
AC_SUBST(ac_c)

AC_OUTPUT( \
	amplot/amcat.awk		amplot/amplot.sh		\
	amplot/Makefile			changer-src/chg-generic.sh	\
	changer-src/hp-changer.sh	changer-src/no-changer.sh	\
	changer-src/rth-changer.pl	changer-src/Makefile		\
	client-src/patch-system.sh	client-src/Makefile		\
	common-src/versuff.c		common-src/Makefile		\
	example/amanda.conf		example/Makefile		\
	man/amadmin.8			man/amanda.8			\
	man/amcheck.8			man/amcleanup.8			\
	man/amdump.8			man/amflush.8			\
	man/amlabel.8			man/Makefile			\
	server-src/amcheckdb.sh		server-src/amcleanup.sh		\
	server-src/amdump.sh		server-src/amfreetapes.sh	\
	server-src/amoverview.pl	server-src/amrmtape.sh		\
	server-src/amtoc.pl		server-src/amverify.sh		\
	server-src/Makefile		Makefile)
