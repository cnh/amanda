dnl Process this file with autoconf to produce a configure script.

AC_INIT(common-src/amanda.h)
AC_CONFIG_AUX_DIR(config-aux)
AC_CANONICAL_SYSTEM
AM_INIT_AUTOMAKE(amanda, 2.4.0)
AM_CONFIG_HEADER(config.h)

AC_PREREQ(2.12)			dnl Minimum Autoconf version required.

dnl host system/type
AC_ARG_PROGRAM

if test -f config.local; then
    echo "running local script ./config.local"
    . ./config.local
fi

dnl
dnl Set the version number of this release of Amanda from the VERSION
dnl string, which is set in AM_INIT_AUTOMAKE.
dnl
changequote(,)
VERSION_MAJOR=`expr "$VERSION" : '\([0-9]*\)'`
VERSION_MINOR=`expr "$VERSION" : '[0-9]*\.\([0-9]*\)'`
VERSION_PATCH=`expr "$VERSION" : '[0-9]*\.[0-9]*\.\([0-9]*\)'`
VERSION_COMMENT=\"`expr "$VERSION" : '[0-9]*\.[0-9]*\.[0-9]*\(.*\)'`\"
changequote([,])

VERSION_SUFFIX="$VERSION"
AC_SUBST(VERSION_MAJOR)
AC_SUBST(VERSION_MINOR)
AC_SUBST(VERSION_PATCH)
AC_SUBST(VERSION_COMMENT)
AC_SUBST(VERSION_SUFFIX)

dnl
dnl runtime and compile time
dnl
SYSPATH="/bin:/usr/bin:/sbin:/usr/sbin:/usr/ucb:/usr/bsd:/etc:/usr/etc"
LOCPATH=`(
    test "x$prefix" = xNONE && prefix=$ac_default_prefix
    test "x$exec_prefix" = xNONE && exec_prefix=${prefix}
    eval echo "$libexecdir:$PATH:/usr/local/bin"
)`
SYSLOCPATH="$SYSPATH:$LOCPATH"
LOCSYSPATH="$LOCPATH:$SYSPATH"

AC_ARG_WITH(includes,
    [   --with-includes=DIR    site header files for readline, etc in DIR],
    [
	case "$withval" in
	"" | y | ye | yes | n | no)
	    AC_MSG_ERROR([*** You must supply an argument to the --with-includes option.])
	  ;;
	esac
	for dir in $withval; do
	    if test -d "$dir"; then
		AMANDA_CPPFLAGS="$AMANDA_CPPFLAGS -I$dir"
	    else
		AC_MSG_WARN([*** Include directory $dir does not exist.])
	    fi
	done
    ],
    AMANDA_CPPFLAGS=
)

AC_ARG_WITH(libraries,
    [   --with-libraries=DIR   site library directories for readline, etc in DIR],
    [
	case "$withval" in
	"" | y | ye | yes | n | no)
	    AC_MSG_ERROR([*** You must supply an argument to the --with-libraries option.])
	  ;;
	esac
	for dir in $withval; do
	    if test -d "$dir"; then
		AMANDA_LDFLAGS="$AMANDA_LDFLAGS -L$dir"
	    else
		AC_MSG_WARN([*** Library directory $dir does not exist.])
	    fi
	done
    ],
    AMANDA_LDFLAGS=
)

AC_ARG_WITH(configdir,
    [   --with-configdir=DIR   runtime config files in DIR [sysconfdir/amanda]],
    [
	case "$withval" in
	"" | y | ye | yes | n | no)
	    AC_MSG_ERROR([*** You must supply an argument to the --with-configdir option.])
	  ;;
	*) CONFIG_DIR="$withval"
	  ;;
	esac
    ],
    : ${CONFIG_DIR=$sysconfdir/amanda}
)
CONFIG_DIR=`(
    test "x$prefix" = xNONE && prefix=$ac_default_prefix
    test "x$exec_prefix" = xNONE && exec_prefix=${prefix}
    eval echo "$CONFIG_DIR"
)`
AC_DEFINE_UNQUOTED(CONFIG_DIR,"$CONFIG_DIR")
AC_SUBST(CONFIG_DIR)

AC_ARG_WITH(indexdir,
    [   --with-indexdir=DIR    index of files dumped go in DIR [localstatedir/amanda/index]],
    [
	case "$withval" in
	"" | y | ye | yes | n | no)
	    AC_MSG_ERROR([*** You must supply an argument to the --with-indexdir option.])
	  ;;
	*) INDEX_DIR="$withval"
	  ;;
	esac
    ],
    : ${INDEX_DIR=$localstatedir/amanda/index}
)
INDEX_DIR=`(
    test "x$prefix" = xNONE && prefix=$ac_default_prefix
    test "x$exec_prefix" = xNONE && exec_prefix=${prefix}
    eval echo "$INDEX_DIR"
)`
AC_DEFINE_UNQUOTED(INDEX_DIR,"$INDEX_DIR")
AC_SUBST(INDEX_DIR)

AC_ARG_WITH(dbdir,
    [   --with-dbdir=DIR       curinfo database in DIR [localstatedir/amanda]],
    [
	case "$withval" in
	"" | y | ye | yes | n | no)
	    AC_MSG_ERROR([*** You must supply an argument to the --with-dbdir option.])
	  ;;
	*) DB_DIR="$withval"
	  ;;
	esac
    ],
    : ${DB_DIR=$localstatedir/amanda}
)
DB_DIR=`(
    test "x$prefix" = xNONE && prefix=$ac_default_prefix
    test "x$exec_prefix" = xNONE && exec_prefix=${prefix}
    eval echo "$DB_DIR"
)`
AC_DEFINE_UNQUOTED(DB_DIR,"$DB_DIR")
AC_SUBST(DB_DIR)

AC_ARG_WITH(logdir,
    [   --with-logdir=DIR      log files go in DIR [localstatedir/amanda]],
    [
	case "$withval" in
	"" | y | ye | yes | n | no)
	    AC_MSG_ERROR([*** You must supply an argument to the --with-logdir option.])
	  ;;
	*) LOG_DIR="$withval"
	  ;;
	esac
    ],
    : ${LOG_DIR=$localstatedir/amanda}
)
LOG_DIR=`(
    test "x$prefix" = xNONE && prefix=$ac_default_prefix
    test "x$exec_prefix" = xNONE && exec_prefix=${prefix}
    eval echo "$LOG_DIR"
)`
AC_DEFINE_UNQUOTED(LOG_DIR,"$LOG_DIR")
AC_SUBST(LOG_DIR)

AC_ARG_WITH(suffixes,
    [   --with-suffixes        install binaries with version string appended to name],
    USE_VERSION_SUFFIXES=$withval,
    : ${USE_VERSION_SUFFIXES=no}
)
case "$USE_VERSION_SUFFIXES" in
y | ye | yes)
    AC_DEFINE(USE_VERSION_SUFFIXES)

    program_suffix="-$VERSION"
    # This is from the output of configure.in.
    if test "$program_transform_name" = s,x,x,; then
	program_transform_name=
    else
	# Double any \ or $.  echo might interpret backslashes.
	cat <<\EOF_SED > conftestsed
s,\\,\\\\,g; s,\$,$$,g
EOF_SED
	program_transform_name="`echo $program_transform_name|sed -f conftestsed`"
	rm -f conftestsed
    fi
    test "$program_prefix" != NONE &&
	program_transform_name="s,^,${program_prefix},; $program_transform_name"
    # Use a double $ so make ignores it.
    test "$program_suffix" != NONE &&
	program_transform_name="s,\$\$,${program_suffix},; $program_transform_name"

    # sed with no file args requires a program.
    test "$program_transform_name" = "" && program_transform_name="s,x,x,"
  ;;
n | no) USE_VERSION_SUFFIXES=no
  ;;
*) AC_MSG_ERROR([*** You must not supply an argument to --with-suffixes option.])
  ;;
esac
AC_SUBST(USE_VERSION_SUFFIXES)

BUILD_CLIENT_PROGS_LIBEXEC="amandad calcsize rundump runtar selfcheck sendbackup sendsize"
BUILD_CLIENT_SCRIPTS_LIBEXEC="patch-system"
BUILD_CLIENT_LIB="libamclient.la"
AMCLIENT_EXTRA_OBJS=

BUILD_RECOVER_PROGS_SBIN="amrecover"

BUILD_CHANGER_PROGS_LIBEXEC="seagate-changer"
BUILD_CHANGER_SCRIPTS_LIBEXEC="chg-generic hp-changer no-changer rth-changer"

BUILD_TAPE_LIB="libamtape.la"

BUILD_SERVER_PROGS_SBIN="amadmin amcheck amflush amlabel amtape"
BUILD_SERVER_PROGS_LIBEXEC="amindexd amtrmidx driver dumper getconf planner reporter taper"
BUILD_SERVER_SCRIPTS_SBIN="amcheckdb amcleanup amdump amoverview amrmtape amtoc amverify"
BUILD_SERVER_LIB="libamserver.la"

BUILD_RESTORE_PROGS_SBIN="amrestore"
BUILD_RESTORE_PROGS_LIBEXEC="amidxtaped"

case "$target" in
    *-hp-*)
	BUILD_CLIENT_SCRIPTS_SBIN=amhpfixdevs
	;;
    *)
	BUILD_CLIENT_SCRIPTS_SBIN=
	;;
esac

AC_SUBST(BUILD_CLIENT_SCRIPTS_SBIN)
AC_SUBST(BUILD_CLIENT_PROGS_LIBEXEC)
AC_SUBST(BUILD_CLIENT_SCRIPTS_LIBEXEC)
AC_SUBST(BUILD_CLIENT_LIB)
AC_SUBST(LIBSRCS_CLIENT)
AC_SUBST(LIBOBJS_CLIENT)
AC_SUBST(AMCLIENT_EXTRA_OBJS)
AC_SUBST(BUILD_RECOVER_PROGS_SBIN)
AC_SUBST(BUILD_CHANGER_PROGS_LIBEXEC)
AC_SUBST(BUILD_CHANGER_SCRIPTS_LIBEXEC)
AC_SUBST(BUILD_TAPE_LIB)
AC_SUBST(BUILD_SERVER_PROGS_SBIN)
AC_SUBST(BUILD_SERVER_PROGS_LIBEXEC)
AC_SUBST(BUILD_SERVER_SCRIPTS_SBIN)
AC_SUBST(BUILD_SERVER_LIB)
AC_SUBST(BUILD_RESTORE_PROGS_SBIN)
AC_SUBST(BUILD_RESTORE_PROGS_LIBEXEC)

AC_ARG_WITH(client-only,
    [   --with-client-only     deprecated, use --without-server],
    [   AC_MSG_ERROR([*** --with-client-only is deprecated, use --without-server instead.])
    ],)
AC_ARG_WITH(server-only,
    [   --with-server-only     deprecated, use --without-client],
    [   AC_MSG_ERROR([*** --with-server-only is deprecated, use --without-client instead.])
    ],)
AC_ARG_WITH(server,
    [   --without-server       do not build server stuff],
    [
	case "$withval" in
	y | ye | yes) NO_SERVER_MODE=false ;;
	n | no) NO_SERVER_MODE=true;;
	*)
	    AC_MSG_ERROR([*** You must not supply an argument to --with-server option.  Maybe you meant --with-indexserver=$withval])
	  ;;
	esac
    ]
)

AC_ARG_WITH(client,
    [   --without-client       do not build client stuff],
    [
	case "$withval" in
	y | ye | yes) NO_CLIENT_MODE=false;;
	n | no) NO_CLIENT_MODE=true;;
	*)
	    AC_MSG_ERROR([*** You must not supply an argument to --with-client option.])
	  ;;
	esac
    ]
)

AC_ARG_WITH(restore,
    [   --without-restore      do not build amrestore nor amidxtaped],
    [
	case "$withval" in
	y | ye | yes) : ;;
	n | no)
	    BUILD_RESTORE_PROGS_SBIN=
	    BUILD_RESTORE_PROGS_LIBEXEC=
	    case "$BUILD_SERVER_LIB" in
	    "") BUILD_TAPE_LIB="";;
	    esac
	  ;;
	*)
	    AC_MSG_ERROR([*** You must not supply an argument to --with-restore option.])
	  ;;
	esac
    ]
)

AC_ARG_WITH(amrecover,
    [   --without-amrecover    do not build amrecover],
    [
	case "$withval" in
	y | ye | yes)
	    if ${NO_CLIENT_MODE-false}; then
		AC_MSG_ERROR([*** --without-client and --with-amrecover are incompatible])
	    fi
	    ;;
	n | no)
	    BUILD_RECOVER_PROGS_SBIN=
	  ;;
	*)
	    AC_MSG_ERROR([*** You must not supply an argument to --with-amrecover option.])
	  ;;
	esac
    ]
)

AC_ARG_WITH(indexserver,
   [   --with-indexserver=HOST default amanda index server [`uname -n`]],
   [
	case "$withval" in
	"" | y | ye | yes | n | no)
	    AC_MSG_ERROR([*** You must supply an argument to the --with-index-server option.])
	  ;;
	*) DEFAULT_SERVER="$withval"
	  ;;
	esac
   ],
   : ${DEFAULT_SERVER=`uname -n`}
)
AC_DEFINE_UNQUOTED(DEFAULT_SERVER,"$DEFAULT_SERVER")
AC_SUBST(DEFAULT_SERVER)

AC_ARG_WITH(force-uid,
    [   --without-force-uid    do not force the uid to --with-user],
    FORCE_USERID="$withval",
    : ${FORCE_USERID=yes}
)
case "$FORCE_USERID" in
y | ye | yes) AC_DEFINE(FORCE_USERID)
  ;;
n | no) :
  ;;
*) AC_MSG_ERROR([*** You must not supply an argument to --with-force-uid option.])
esac

AC_ARG_WITH(user,
    [   --with-user=USER       force execution to USER on client systems [${LOGIN-${USER-${LOGNAME-`whoami`}}}]],
    [
	case "$withval" in
	"" | y | ye | yes | n | no)
	    AC_MSG_ERROR([*** You must supply an argument to the --with-user option.])
	  ;;
	*) CLIENT_LOGIN="$withval"
	  ;;
	esac
    ],
    : ${CLIENT_LOGIN=${LOGIN-${USER-${LOGNAME-`whoami`}}}}
)
AC_DEFINE_UNQUOTED(CLIENT_LOGIN,"$CLIENT_LOGIN")
AC_SUBST(CLIENT_LOGIN)

AC_ARG_WITH(group,
    [   --with-group=USER      group allowed to execute setuid-root programs [`(id 2>/dev/null; echo amanda) | sed -e 's/^.*gid=[0-9]*(\([^)]*\)).*/\1/' | grep -v = | head -1`]],
    [
	case "$withval" in
	"" | y | ye | yes | n | no)
	    AC_MSG_ERROR([*** You must supply an argument to the --with-group option.])
	  ;;
	*) SETUID_GROUP="$withval"
	  ;;
	esac
    ],
    [[
	: ${SETUID_GROUP=`(id 2>/dev/null; echo amanda) | sed -e 's/^.*gid=[0-9]*(\([^)]*\)).*/\1/' | grep -v = | head -1`}
    ]]
)
AC_SUBST(SETUID_GROUP)

AC_ARG_WITH(rundump,
    [   --with-rundump         use rundump (setuid-root) to invoke dump],
    USE_RUNDUMP="$withval",
    : ${USE_RUNDUMP=no}
)
case "$USE_RUNDUMP" in
n | no) : ;;
y |  ye | yes) AC_DEFINE(USE_RUNDUMP)
  ;;
*) AC_MSG_ERROR([*** You must not supply an argument to --with-rundump option.])
  ;;
esac

AC_ARG_WITH(config,
   [   --with-config=CONFIG   default configuration [DailySet1]],
   [
	case "$withval" in
	"" | y | ye | yes | n | no)
	    AC_MSG_ERROR([*** You must supply an argument to the --with-config option.])
	  ;;
	*) DEFAULT_CONFIG="$withval"
	  ;;
	esac
   ],
   : ${DEFAULT_CONFIG=DailySet1}
)
AC_DEFINE_UNQUOTED(DEFAULT_CONFIG,"$DEFAULT_CONFIG")
AC_SUBST(DEFAULT_CONFIG)

AC_ARG_WITH(tape-server,
    [   --with-tape-server=HOST default restoring tape server is HOST [--with-server]],
    [
	case "$withval" in
	"" | y | ye | yes | n | no)
	    AC_MSG_ERROR([*** You must supply an argument to the --with-tape-server option.])
	  ;;
	*) DEFAULT_TAPE_SERVER="$withval"
	  ;;
	esac
    ],
    : ${DEFAULT_TAPE_SERVER=$DEFAULT_SERVER}
)
AC_DEFINE_UNQUOTED(DEFAULT_TAPE_SERVER,"$DEFAULT_TAPE_SERVER")
AC_SUBST(DEFAULT_TAPE_SERVER)

AC_ARG_WITH(tape-device,
    [   --with-tape-device=ARG restoring tape server HOST's no rewinding tape drive],
    [
	case "$withval" in
	"" | y | ye | yes | n | no)
	    AC_MSG_ERROR([*** You must supply an argument to the --with-tape-device option.])
	  ;;
	*) DEFAULT_TAPE_DEVICE="$withval"
	  ;;
	esac
    ],
    : ${DEFAULT_TAPE_DEVICE=/dev/null}
)
AC_DEFINE_UNQUOTED(DEFAULT_TAPE_DEVICE,"$DEFAULT_TAPE_DEVICE")
AC_SUBST(DEFAULT_TAPE_DEVICE)

AC_ARG_WITH(rew-tape,
    [   --with-rew-tape=ARG    rewinding tape device for changer scripts is ARG],
    [
	case "$withval" in
	"" | y | ye | yes | n | no)
	    AC_MSG_ERROR([*** You must supply an argument to the --with-rew-tape option.])
	  ;;
	*) TAPE_REWIND_DEVICE="$withval"
	  ;;
	esac
    ],
    : ${TAPE_REWIND_DEVICE=/dev/null}
)
AC_DEFINE_UNQUOTED(TAPE_REWIND_DEVICE,"$TAPE_REWIND_DEVICE")
AC_SUBST(TAPE_REWIND_DEVICE)

AC_ARG_WITH(norew-tape,
    [   --with-norew-tape=ARG  norewinding tape device for changer scripts is ARG],
    [
	case "$withval" in
	"" | y | ye | yes | n | no)
	    AC_MSG_ERROR([*** You must supply an argument to the --with-norew-tape option.])
	  ;;
	*) TAPE_NO_REWIND_DEVICE="$withval"
	  ;;
	esac
    ],
    : ${TAPE_NO_REWIND_DEVICE=/dev/null}
)
AC_DEFINE_UNQUOTED(TAPE_NO_REWIND_DEVICE,"$TAPE_NO_REWIND_DEVICE")
AC_SUBST(TAPE_NO_REWIND_DEVICE)

AC_ARG_WITH(fqdn,
    [   --with-fqdn            use FQDN's to backup multiple networks],
    USE_FQDN=$withval,
    : ${USE_FQDN=no}
)
case "$USE_FQDN" in
n | no) : ;;
y |  ye | yes) AC_DEFINE(USE_FQDN)
  ;;
*) AC_MSG_ERROR([*** You must not supply an argument to --with-fqdn option.])
  ;;
esac

AC_ARG_WITH(smbclient,
    [   --with-smbclient[=PATH] Compile in Samba PC backup support, where PATH is the location of smbclient],
    [
	case "$withval" in
	y | ye | yes)
	    AC_PATH_PROGS(SAMBA_CLIENT,smbclient,,$LOCSYSPATH)
	    if test -z "$SAMBA_CLIENT"; then
		AC_MSG_ERROR([*** please specify --with-smbclient=/some/dir/smbclient])
	    fi
	  ;;
	n | no) unset SAMBA_CLIENT
	  ;;
	*) SAMBA_CLIENT="$withval"
	  ;;
	esac
    ]
)
if test "$SAMBA_CLIENT"; then
    LIBSRCS_CLIENT="$LIBSRCS_CLIENT findpass.c"
    LIBOBJS_CLIENT="$LIBOBJS_CLIENT findpass.lo"
    AC_DEFINE_UNQUOTED(SAMBA_CLIENT,"$SAMBA_CLIENT")
fi

AC_ARG_WITH(gnutar-listed-incremental,
   [   --with-gnutar-listed-incremental=DIR  gnutar directory lists go in DIR [localstatedir/amanda/gnutar-lists]],
   [
	case "$withval" in
	    n | no)		unset GNUTAR_LIST_DIR ;;
	    y | ye | yes)	: ${GNUTAR_LIST_DIR=$localstatedir/amanda/gnutar-lists} ;;
	    *)			GNUTAR_LIST_DIR="$withval" ;;
	esac
    ],
    : ${GNUTAR_LIST_DIR=$localstatedir/amanda/gnutar-lists}
)
if test "$GNUTAR_LIST_DIR"; then
    tmp=`eval echo $GNUTAR_LIST_DIR`
    AC_DEFINE_UNQUOTED(GNUTAR_LISTED_INCREMENTAL_DIR,"$tmp")
fi

AC_ARG_WITH(bsd-security,
    [   --with-bsd-security    use BSD rsh/rlogin style security],
    BSD_SECURITY=$withval,
    : ${BSD_SECURITY=no}
)
case "$BSD_SECURITY" in
n | no) : ;;
y |  ye | yes) AC_DEFINE(BSD_SECURITY)
  ;;
*) AC_MSG_ERROR([*** You must not supply an argument to --with-bsd-security option.])
  ;;
esac

AC_ARG_WITH(amandahosts,
    [   --with-amandahosts     use .amandahosts instead .rhosts],
    USE_AMANDAHOSTS=$withval,
    : ${USE_AMANDAHOSTS=no}
)
case "$USE_AMANDAHOSTS" in
n | no) : ;;
y |  ye | yes) AC_DEFINE(USE_AMANDAHOSTS)
  ;;
*) AC_MSG_ERROR([*** You must not supply an argument to --with-amandahosts option.])
  ;;
esac

dnl Specify --with-krb4 if kerberos bits are in somewhere other than the
dnl listed KRB4_SPOTS.  We only compile kerberos support in if the right
dnl files are there.

AC_ARG_WITH(krb4-security,
    [   --with-krb4=DIR   kerberos bits can be found here],
    KRB4_SPOTS="$withval",
    : ${KRB4_SPOTS="/usr/kerberos /usr/cygnus /usr /opt/kerberos"}
)

AC_MSG_CHECKING(kerberos4 bits)
if test -f common-src/krb4-security.c ; then
    for dir in $KRB4_SPOTS; do
	if test "$KRB4_SECURITY" != "yes" -a -f ${dir}/lib/libkrb.a ; then
	    AC_MSG_RESULT(found in $dir)
	    KRB4_SECURITY=yes
	    AC_DEFINE(KRB4_SECURITY)
	    KRB4INCLUDES=-I$dir/include
	    KRB4LDFLAGS=-L$dir/lib
	    KRB4LIBS="-lkrb -ldes"
	    KRB4COMMON_C=krb4-security.c
	    KRB4COMMON_O=krb4-security.o
	    KRB4CLIENT_C=sendbackup-krb4.c
	    KRB4CLIENT_O=sendbackup-krb4.o
	fi
    done

    if test "$KRB4LDFLAGS" = "" ; then
	AC_MSG_RESULT(no libkrb.a)
    fi
else
    AC_MSG_RESULT(no)
fi

AC_ARG_WITH(server-principal,
    [     --with-server-principal=ARG    server host principal  ["amanda"]],
    [
	case "$withval" in
	"" | y | ye | yes | n | no)
	    AC_MSG_ERROR([*** You must supply an argument to the --with-server-principal option.])
	  ;;
	*)
	    SERVER_HOST_PRINCIPLE="$withval"
	  ;;
	esac
    ],
    : ${SERVER_HOST_PRINCIPLE="amanda"}
)
AC_DEFINE_UNQUOTED(SERVER_HOST_PRINCIPLE,"$SERVER_HOST_PRINCIPLE")

AC_ARG_WITH(server-instance,
    [     --with-server-instance=ARG     server host instance   ["amanda"]],
    [
	case "$withval" in
	"" | y | ye | yes | n | no)
	    AC_MSG_ERROR([*** You must supply an argument to the --with-server-instance option.])
	  ;;
	*) SERVER_HOST_INSTANCE="$withval"
	  ;;
	esac
    ],
    : ${SERVER_HOST_INSTANCE="amanda"}
)
AC_DEFINE_UNQUOTED(SERVER_HOST_INSTANCE,"$SERVER_HOST_INSTANCE")

AC_ARG_WITH(server-keyfile,
    [     --with-server-keyfile=ARG      server host key file   ["/.amanda"]],
    [
	case "$withval" in
	"" | y | ye | yes | n | no)
	    AC_MSG_ERROR([*** You must supply an argument to the --with-server-keyfile option.])
	  ;;
	*) SERVER_HOST_KEY_FILE="$withval"
	  ;;
	esac
    ],
    : ${SERVER_HOST_KEY_FILE="/.amanda"}
)
AC_DEFINE_UNQUOTED(SERVER_HOST_KEY_FILE,"$SERVER_HOST_KEY_FILE")

AC_ARG_WITH(client-principal,
    [     --with-client-principal=ARG    client host principal  ["rcmd"]],
    [
	case "$withval" in
	"" | y | ye | yes | n | no)
	    AC_MSG_ERROR([*** You must supply an argument to the --with-client-principal option.])
	  ;;
	*) CLIENT_HOST_PRINCIPLE="$withval"
	  ;;
	esac
    ],
    : ${CLIENT_HOST_PRINCIPLE="rcmd"}
)
AC_DEFINE_UNQUOTED(CLIENT_HOST_PRINCIPLE,"$CLIENT_HOST_PRINCIPLE")

AC_ARG_WITH(client-instance,
    [     --with-client-instance=ARG     client host instance   [HOSTNAME_INSTANCE]],
    [
	case "$withval" in
	"" | y | ye | yes | n | no)
	    AC_MSG_ERROR([*** You must supply an argument to the --with-client-instance option.])
	  ;;
	*) CLIENT_HOST_INSTANCE="$withval"
	  ;;
	esac
    ],
    : ${CLIENT_HOST_INSTANCE=HOSTNAME_INSTANCE}
)
AC_DEFINE_UNQUOTED(CLIENT_HOST_INSTANCE,$CLIENT_HOST_INSTANCE)

AC_ARG_WITH(client-keyfile,
    [     --with-client-keyfile=ARG      client host key file   [KEYFILE]],
    [
	case "$withval" in
	"" | y | ye | yes | n | no)
	    AC_MSG_ERROR([*** You must supply an argument to the --with-client-keyfile option.])
	  ;;
	*) CLIENT_HOST_KEY_FILE="$withval"
	  ;;
	esac
    ],
    : ${CLIENT_HOST_KEY_FILE=KEYFILE}
)
AC_DEFINE_UNQUOTED(CLIENT_HOST_KEY_FILE,$CLIENT_HOST_KEY_FILE)

AC_ARG_WITH(ticket-lifetime,
    [     --with-ticket-lifetime=ARG     ticket lifetime        [128]],
    [
	case "$withval" in
	"" | y | ye | yes | n | no)
	    AC_MSG_ERROR([*** You must supply an argument to the --with-ticket-lifetime option.])
	  ;;
	*) TICKET_LIFETIME="$withval"
	  ;;
	esac
    ],
    : ${TICKET_LIFETIME=128}
)
AC_DEFINE_UNQUOTED(TICKET_LIFETIME,$TICKET_LIFETIME)

AC_ARG_WITH(db,
    [   --with-db={db,dbm,gdbm,ndbm,text} use the given library for dbm_open],
    [
	case "$withval" in
	"" | y | ye | yes | n | no)
	    AC_MSG_ERROR([*** You must supply an argument to the --with-db option.])
	  ;;
	*) DB_STYLE="$withval"
	  ;;
	esac
    ]
)
if test "$DB_STYLE"; then
    case "$DB_STYLE" in
	db | dbm | gdbm | ndbm | text)	;;
	*)
	    AC_MSG_ERROR([*** Unknown argument $DB_STYLE given to --with-db.  Choose from db, dbm, gdbm, ndbm, text.])
	    DB_STYLE=
	    ;;
    esac
fi

AC_ARG_WITH(mmap,
    [   --with-mmap            force use of mmap instead of shared memory support],
    FORCE_MMAP=$withval,
    : ${FORCE_MMAP=no}
)
case "$FORCE_MMAP" in
y | ye | yes | n | no) : ;;
*) AC_MSG_ERROR([*** You must not supply an argument to --with-mmap.])
  ;;
esac

AC_ARG_WITH(buffered-dump,
    [   --with-buffered-dump   buffer the dumping sockets on the server for speed],
    DUMPER_SOCKET_BUFFERING=$withval,
    : ${DUMPER_SOCKET_BUFFERING=no}
)
case "$DUMPER_SOCKET_BUFFERING" in
n | no) :
  ;;
y | ye | yes) AC_DEFINE(DUMPER_SOCKET_BUFFERING)
  ;;
*) AC_MSG_ERROR([*** You must not supply an argument to --with-buffered-dump.])
  ;;
esac

AC_ARG_WITH(assertions,
    [   --with-assertions      compile assertions into code],
    ASSERTIONS="$withval",
    : ${ASSERTIONS=no}
)
case "$ASSERTIONS" in
n | no) : ;;
y |  ye | yes) AC_DEFINE(ASSERTIONS)
  ;;
*) AC_MSG_ERROR([*** You must not supply an argument to --with-assertions option.])
  ;;
esac

AC_ARG_WITH(debugging,
    [   --without-debugging    do not record runtime debugging information into /tmp],
    DEBUGGING="$withval",
    : ${DEBUGGING=yes}
)
case "$DEBUGGING" in
n | no) : ;;
y |  ye | yes) AC_DEFINE(DEBUG_CODE)
  ;;
*) AC_MSG_ERROR([*** You must not supply an argument to --without-debugging option.])
  ;;
esac

AC_ARG_WITH(pid-debug-files,
    [   --with-pid-debug-files append pid's to program's /tmp debugging filenames],
    DEBUG_FILE_WITH_PID="$withval",
    : ${DEBUG_FILE_WITH_PID=no}
)
case "$DEBUG_FILE_WITH_PID" in
n | no) : ;;
y |  ye | yes) AC_DEFINE(DEBUG_FILE_WITH_PID)
  ;;
*) AC_MSG_ERROR([*** You must not supply an argument to --with-pid-debug-files option.])
  ;;
esac

AC_ARG_WITH(testing,
    [   --with-testing         use an alternate service name],
    TESTING="$withval",
    : ${TESTING=no}
)
case "$TESTING" in
n | no) : ;;
y |  ye | yes) AC_DEFINE(USE_TEST_SERVICE)
  ;;
*) AC_MSG_ERROR([*** You must not supply an argument to --with-testing option.])
  ;;
esac

(
    test "x$prefix" = xNONE && prefix=$ac_default_prefix
    test "x$exec_prefix" = xNONE && exec_prefix=${prefix}

    tmp=`eval echo "$bindir"`
    AC_DEFINE_UNQUOTED(bindir,"$tmp")

    tmp=`eval echo "$sbindir"`
    AC_DEFINE_UNQUOTED(sbindir,"$tmp")

    tmp=`eval echo "$libexecdir"`
    AC_DEFINE_UNQUOTED(libexecdir,"$tmp")

    tmp=`eval echo $mandir`
    AC_DEFINE_UNQUOTED(mandir,"$tmp")
)

AC_CHECK_PROG(CC, gcc, gcc)
if test -z "$CC"; then
  AC_CHECK_PROG(CC, cc, cc, , , /usr/ucb/cc)
  test -z "$CC" && AC_MSG_ERROR([no acceptable cc found in \$PATH])
fi

dnl Set the order of dump programs to look for.  Finding the proper file
dnl system dumping program is problematic.  Some systems, notably HP-UX
dnl and AIX, have both the backup and dump programs.  HP-UX can't use the
dnl the backup program while AIX systems can't use the dump program.  So
dnl a variable is set up here to specify the order of dump programs to
dnl search for on the system.
DUMP_PROGRAMS="ufsdump vdump dump backup"

case "$target" in
    *-dec-osf*)
			# setpgrp takes void
			case "$target" in *-dec-osf[[123]]*)
				ac_cv_func_setpgrp_void=yes;;
			esac
			AC_DEFINE(STATFS_OSF1)
			;;
    *-netbsd*)
			;;
    *-freebsd*)
			;;
    *-hp-*)
			MT_FILE_FLAG="-t"
			case "$CC" in
			    *gcc*)
				;;
			    *cc*)
				AMANDA_CFLAGS="-Ae $AMANDA_CFLAGS"
				;;
			esac
			;;
  *-ibm-aix*)
			DUMP_PROGRAMS="backup "$DUMP_PROGRAMS
			AC_DEFINE(AIX_TAPEIO)
			AC_DEFINE(AIX_BACKUP)
			;;
  m88k-motorola-sysv4)
			;;
  *-nextstep3)
			;;
  *-pc-linux-*)
			;;
  *-sgi-irix3*)
			dnl The old cc won't work!
			CC=gcc
			;;
  *-sgi-irix4*)
			;;
  *-sgi-irix5*)
			;;
  *-sgi-irix6*)
			;;
  *-solaris2*)
			;;
  *-sun-sunos4.1*)
			;;
  sparc-unknown-linux-gnu)
			;;
  *-ultrix*)
			AC_DEFINE(STATFS_ULTRIX)
			AC_DEFINE(DUMP_RETURNS_1)
			;;
  i386-pc-isc4*)
			AC_DEFINE(XENIX_TAPEIO)
			;;
  *)
			cat <<END

*****
This machine, target type $target, is not known
to be fully supported by this configure script.  If the
installation of Amanda on this system succeeds or needed
any patches, please email amanda-hackers@cs.umd.edu with
the patches or an indication of the sucess or failure of
the Amanda installation on your system.
*****

END
		;;
esac

dnl Set the default CFLAGS to -g if CFLAGS has not been set.
if test "x" = "x$CFLAGS"; then
    CFLAGS=-g
fi
OLD_CFLAGS="$CFLAGS"
AMANDA_CFLAGS="$AMANDA_CFLAGS $KRB4INCLUDES"
AMANDA_CPPFLAGS="$AMANDA_CPPFLAGS $KRB4INCLUDES"
AMANDA_LDFLAGS="$AMANDA_LDFLAGS $KRB4INCLUDES"
CFLAGS="$CFLAGS $AMANDA_CPPFLAGS $AMANDA_CFLAGS"
CPPFLAGS="$CPPFLAGS $AMANDA_CPPFLAGS"
LDFLAGS="$LDFLAGS $AMANDA_LDFLAGS $KRB4LDFLAGS"
AC_SUBST(AMANDA_CFLAGS)


dnl This specifies the flag for mt that tells mt the proper tape drive
dnl to use.
: ${MT_FILE_FLAG="-f"}
AC_SUBST(MT_FILE_FLAG)
AC_DEFINE_UNQUOTED(MT_FILE_FLAG, "$MT_FILE_FLAG")

dnl Check for programs.
AC_PATH_PROGS(AR,ar,,$LOCSYSPATH)

BUILD_AMPLOT_SBIN="amplot"
BUILD_AMPLOT_LIBEXEC="amcat.awk amplot.awk amplot.g amplot.gp"
AC_SUBST(BUILD_AMPLOT_SBIN)
AC_SUBST(BUILD_AMPLOT_LIBEXEC)

AC_PROG_AWK
AMANDA_PROG_AWK_VAR
if test "$amanda_cv_awk_var_assignment" = no; then
    BUILD_AMPLOT_SBIN=
    BUILD_AMPLOT_LIBEXEC=
    AC_MSG_WARN([*** Your $awk cannot do command line variable assignment.  Amplot will not be installed.])
fi

AC_PROG_YACC
AC_PATH_PROGS(CAT,cat,,$LOCSYSPATH)
if test -z "$CAT"; then
    CAT=cat
fi
AC_PATH_PROGS(COMPRESS,compress,,$LOCSYSPATH)
AC_PATH_PROGS(DD,dd,,$LOCSYSPATH)
AC_PATH_PROGS(EGREP,egrep,,$LOCSYSPATH)

AC_PATH_PROGS(GNUPLOT,gnuplot,,$LOCSYSPATH)
if test -z "$GNUPLOT"; then
    BUILD_AMPLOT_SBIN=
    BUILD_AMPLOT_LIBEXEC=
    AC_MSG_WARN([*** You do not have gnuplot.  Amplot will not be installed.])
fi

AC_PATH_PROGS(GREP,grep,,$LOCSYSPATH)
if test -z "$GREP"; then
    GREP=grep
fi
AC_DEFINE_UNQUOTED(GREP,"$GREP")

AC_PATH_PROGS(GNUTAR,gtar gnutar tar,,$LOCSYSPATH)
if test "$GNUTAR"; then
  case "`\"$GNUTAR\" --version 2>&1`" in
   *GNU*tar*)
		AC_DEFINE_UNQUOTED(GNUTAR,"$GNUTAR")
		;;
   *)
		AC_MSG_WARN([*** $GNUTAR is not GNU tar, so it will not be used.])
		GNUTAR=
		;;
  esac
fi

AC_PATH_PROGS(GZIP,gzip,,$LOCSYSPATH)
if test "$GZIP"; then
    AC_DEFINE(HAVE_GZIP)
    AC_DEFINE_UNQUOTED(COMPRESS_PATH,"$GZIP")
    AC_DEFINE_UNQUOTED(COMPRESS_SUFFIX,".gz")
    AC_DEFINE_UNQUOTED(COMPRESS_FAST_OPT,"--fast")
    AC_DEFINE_UNQUOTED(COMPRESS_BEST_OPT,"--best")
    AC_DEFINE_UNQUOTED(UNCOMPRESS_PATH,"$GZIP")
    AC_DEFINE_UNQUOTED(UNCOMPRESS_OPT,"-dc")
else
    if test "$COMPRESS"; then
	AC_DEFINE_UNQUOTED(COMPRESS_PATH,"$COMPRESS")
	AC_DEFINE_UNQUOTED(COMPRESS_SUFFIX,".Z")
	AC_DEFINE_UNQUOTED(COMPRESS_FAST_OPT,"-f")
	AC_DEFINE_UNQUOTED(COMPRESS_BEST_OPT,"-f")
	AC_DEFINE_UNQUOTED(UNCOMPRESS_PATH,"$COMPRESS")
	AC_DEFINE_UNQUOTED(UNCOMPRESS_OPT,"-dc")
    else
	dnl If we have to use cat, we don't define COMPRESS_FAST_OPT,
	dnl COMPRESS_BEST_OPT, or UNCOMPRESS_OPT as "" since cat will look
	dnl look for a file by the name of "".
	AC_MSG_WARN([*** Cannot find either gzip or compress.  Using cat. ***])
	AC_DEFINE_UNQUOTED(COMPRESS_PATH,"$CAT")
	AC_DEFINE_UNQUOTED(COMPRESS_SUFFIX,"")
	AC_DEFINE_UNQUOTED(UNCOMPRESS_PATH,"$CAT")
    fi
fi

AC_PATH_PROGS(MAILER,Mail mailx)
if test "$MAILER"; then
    AC_DEFINE_UNQUOTED(MAILER,"$MAILER")
fi

AC_PATH_PROGS(MT,mt,,$LOCSYSPATH)

AC_PATH_PROGS(MTX,mtx,,$LOCSYSPATH)
if test "$MTX"; then
    MTX=mtx
fi

AC_PATH_PROGS(PCAT,pcat,,$LOCSYSPATH)
AC_PATH_PROGS(PERL,perl5 perl,,$LOCSYSPATH)

AC_PROG_RANLIB

dnl AC_PATH_PROGS(MAKEINFO,makeinfo,,$LOCSYSPATH)
dnl AC_PATH_PROGS(TEXI2DVI,texi2dvi,,$LOCSYSPATH)
SHELL=
AC_PATH_PROGS(SHELL,sh,,$LOCSYSPATH)

AC_PATH_PROGS(DUMP,$DUMP_PROGRAMS,,$SYSLOCPATH)
if test "$DUMP"; then
    AC_DEFINE_UNQUOTED(DUMP,"$DUMP")
    AC_CACHE_CHECK(
	[whether $DUMP supports -E for estimates],
	amanda_cv_dump_estimate,
	[
	    $DUMP 9Ef /dev/null /dev/null/invalid/fs >conftest.d-E 2>&1
	    $DUMP 9f /dev/null /dev/null/invalid/fs 2>&1 |
		$EGREP -v 'Dumping|Date of' >conftest.d
	    if diff conftest.d-E conftest.d >/dev/null 2>&1; then
		amanda_cv_dump_estimate=yes
	    else
		amanda_cv_dump_estimate=no
	    fi
	    rm -f conftest.d conftest.d-E
	])
    if test "$amanda_cv_dump_estimate" = yes; then
	AC_DEFINE(HAVE_DUMP_ESTIMATE)
    fi
fi
case "$DUMP" in
    *vdump*)
	AC_DEFINE(OSF1_VDUMP)
	AC_DEFINE(USE_RUNDUMP)
	AC_MSG_WARN([*** enabling rundump because vdump must run as root])
	;;
esac

AC_PATH_PROGS(RESTORE,ufsrestore vrestore restore,,$SYSLOCPATH)
if test "$RESTORE"; then
    AC_DEFINE_UNQUOTED(RESTORE,"$RESTORE")
fi

AC_PATH_PROGS(XFSDUMP,xfsdump,,$SYSLOCPATH)
if test "$XFSDUMP"; then
    AC_DEFINE_UNQUOTED(XFSDUMP,"$XFSDUMP")
    AC_DEFINE(USE_RUNDUMP)
    AC_MSG_WARN([*** enabling rundump because xfsdump must be run as root])
fi
AC_PATH_PROGS(XFSRESTORE,xfsrestore,,$SYSLOCPATH)
if test "$XFSRESTORE"; then
    AC_DEFINE_UNQUOTED(XFSRESTORE,"$XFSRESTORE")
fi

AC_PATH_PROGS(VXDUMP,vxdump,,$SYSLOCPATH)
if test "$VXDUMP"; then
    AC_DEFINE_UNQUOTED(VXDUMP,"$VXDUMP")
fi
AC_PATH_PROGS(VXRESTORE,vxrestore,,$SYSLOCPATH)
if test "$VXRESTORE"; then
    AC_DEFINE_UNQUOTED(VXRESTORE,"$VXRESTORE")
fi

dnl Handle all of the substitutions to make amplot work.
if test "$PCAT"; then
    AMPLOT_CAT_PACK="if(o==\"z\")print \"$PCAT\"; else"
else
    AMPLOT_CAT_PACK=
fi
if test "$COMPRESS"; then
    AMPLOT_COMPRESS=$COMPRESS
    AMPLOT_CAT_COMPRESS="if(o==\"Z\")print \"$COMPRESS -dc\"; else"
else
    AMPLOT_CAT_COMPRESS=
fi
if test "$GZIP"; then
    AMPLOT_COMPRESS=$GZIP
    AMPLOT_CAT_GZIP="if(o==\"gz\")print \"$GZIP -dc\"; else"
else
    AMPLOT_CAT_GZIP=
fi
AC_SUBST(AMPLOT_COMPRESS)
AC_SUBST(AMPLOT_CAT_GZIP)
AC_SUBST(AMPLOT_CAT_COMPRESS)
AC_SUBST(AMPLOT_CAT_PACK)

dnl Empty GZIP so that make dist works.
GZIP=

AC_MSG_CHECKING(for rewinding tape device)
dnl Check for the /dev/rmt directory and use what's in there.  Otherwise
dnl look for tape devices in /dev.  For the devices in /dev/rmt, we want
dnl to use the Berkeley behavior of reading the first record of the next
dnl tape file after 0 bytes are returned upon reading to the next tape
dnl mark, instead of returning an error.  Look for devices that have a
dnl 'b' in their name.
tape_dev=/dev/null
nr_tape_dev=/dev/null
if test -d /dev/rmt; then
    dnl See if we can find two devices, one being the norewind version
    dnl of the other.  Devices in this directory are normally a digit
    dnl followed by some characters.  We also want the Berkely behavior,
    dnl since Amanda needs it for amrestore.
    for num in 9 8 7 6 5 4 3 2 1 0; do
	td=/dev/rmt/${num}b
	ntd=/dev/rmt/${num}bn
	if test -r $td -a -r $ntd; then
	    tape_dev=$td
	    nr_tape_dev=$ntd
	fi
    done
else
    dnl Look for tape devices in /dev.
    for num in 9 8 7 6 5 4 3 2 1 0; do
	td=/dev/rst${num}
	ntd=/dev/nrst${num}
	if test -r $td -a -r $ntd; then
	    tape_dev=$td
	    nr_tape_dev=$ntd
	fi
    done
fi

if test "$TAPE_REWIND_DEVICE" = /dev/null; then
    TAPE_REWIND_DEVICE=$tape_dev
fi

if test "$TAPE_NO_REWIND_DEVICE" = /dev/null; then
    TAPE_NO_REWIND_DEVICE=$nr_tape_dev
fi

AC_MSG_RESULT($TAPE_REWIND_DEVICE)

AC_MSG_CHECKING(for no rewinding tape device)
AC_MSG_RESULT($TAPE_NO_REWIND_DEVICE)

dnl Checks for compilers, typedefs, structures, and compiler characteristics.
AM_PROG_LIBTOOL
AC_PROG_CC
AC_PROG_GCC_TRADITIONAL
AC_C_CONST
AMANDA_C_VOLATILE
AC_TYPE_OFF_T
AC_TYPE_PID_T
AC_TYPE_SIZE_T
AC_TYPE_UID_T
AC_TYPE_SIGNAL
AC_STRUCT_TM
AC_STRUCT_ST_BLOCKS
AC_PROG_LEX
AC_DECL_YYTEXT

dnl Checks for header files.
AC_HEADER_STDC
AC_HEADER_DIRENT
AC_HEADER_SYS_WAIT
AC_HEADER_TIME
AC_CHECK_HEADERS(\
	asm/byteorder.h \
	chio.h \
	sys/chio.h \
	db.h \
	dbm.h \
	fcntl.h \
	fstab.h \
	grp.h \
	history.h \
	mntent.h \
	mnttab.h \
	ndbm.h \
	netdb.h \
	readline.h \
	readline/history.h \
	readline/readline.h \
	stdlib.h \
	string.h \
	sys/fcntl.h \
	sys/file.h \
	sys/ioctl.h \
	sys/ipc.h \
	sys/mman.h \
	sys/mntent.h \
	sys/mount.h \
	sys/param.h \
	sys/select.h \
	sys/shm.h \
	sys/stat.h \
	sys/statfs.h \
	sys/statvfs.h \
	sys/time.h \
	sys/types.h \
	sys/vfs.h \
	sys/vfstab.h \
	syslog.h \
	unistd.h)

dnl Do not build seagate-changer if we can't find chio.h.
if test "$ac_cv_header_chio_h" = no -o "$ac_cv_header_sys_chio_h" = no; then
    BUILD_CHANGER_PROGS_LIBEXEC=
fi

dnl cur_colr is on some HP's
AC_CHECK_LIB(cur_colr,main)

AC_CHECK_LIB(intl,main)

dnl Make sure we don't use -lnsl and -lsun on Irix systems.
case "$target" in
    *sgi-irix*)
			AC_CHECK_LIB(socket,main)
			;;
    *)
			AC_CHECK_LIB(nsl,main)
			AC_CHECK_LIB(socket,main)
			AC_CHECK_LIB(sun,main)
			;;
esac

AC_CHECK_LIB(termcap,main)
if test "$ac_cv_lib_termcap_main" = yes; then
    AC_CHECK_LIB(readline,main)
    if test "$ac_cv_lib_readline_main" != yes; then
	AC_MSG_WARN([*** libreadline not found! No history and command line editing in amrecover!])
    fi
else
    AC_MSG_WARN([*** libtermcap not found! No history and command line editing in amrecover!])
fi
dnl

dnl Check if the system does support the user requested database library.
dnl Begin by checking for the header file.  If it is not there, then do
dnl not use the library.  If the header file is there, then try to link
dnl against the library.   If it's not there, then try to link using
dnl just the -lc library.  If the link against -lc fails, then do not
dnl use this library.
DB_HEADER=
DB_LIB=

dnl Testing if libc contains the dbm_open routine is tested for a
dnl lot of times below.  We do it once here now.
save_LIBS="$LIBS"
AC_CHECK_LIB(c,dbm_open)
LIBS="$save_LIBS"

case "$DB_STYLE" in
    db)
	if test "$ac_cv_header_db_h" = yes; then
	    AC_CHECK_LIB(db,main)
	    if test "$ac_cv_lib_db_main" = yes; then
		AC_CHECK_LIB(db,dbm_open)
		if test "$ac_cv_lib_db_dbm_open" = yes; then
		    DB_HEADER=db.h
		    DB_LIB=db
		else
		    DB_STYLE=
		    AC_MSG_WARN([*** db database library requested but dbm_open not found in -ldb.])
		fi
	    else
		if test "$ac_cv_lib_c_dbm_open" = yes; then
		    DB_HEADER=db.h
		    DB_LIB=c
		else
		    DB_STYLE=
		    AC_MSG_WARN([*** db library requested but -ldb doesn't exist and dbm_open cannot be found.])
		fi
	    fi
	else
	    DB_STYLE=
	    AC_MSG_WARN([*** db database library requested but db.h not found.])
	fi
	;;

    dbm)
	if test "$ac_cv_header_dbm_h" = yes; then
	    AC_CHECK_LIB(dbm,main)
	    if test "$ac_cv_lib_dbm_main" = yes; then
		AC_CHECK_LIB(dbm,dbm_open)
		if test "$ac_cv_lib_dbm_dbm_open" = yes; then
		    DB_HEADER=dbm.h
		    DB_LIB=dbm
		else
		    DB_STYLE=
		    AC_MSG_WARN([*** dbm database library requested but dbm_open not found in -ldbm.])
		fi
	    else
		if test "$ac_cv_lib_c_dbm_open" = yes; then
		    DB_HEADER=dbm.h
		    DB_LIB=c
		else
		    DB_STYLE=
		    AC_MSG_WARN([*** dbm library requested but -ldbm doesn't exist and dbm_open cannot be found.])
		fi
	    fi
	else
	    DB_STYLE=
	    AC_MSG_WARN([*** dbm database library requested but dbm.h not found.])
	fi
	;;

    gdbm)
	if test "$ac_cv_header_ndbm_h" = yes; then
	    AC_CHECK_LIB(gdbm,main)
	    if test "$ac_cv_lib_gdbm_main" = yes; then
		AC_CHECK_LIB(gdbm,dbm_open)
		if test "$ac_cv_lib_gdbm_dbm_open" = yes; then
		    DB_HEADER=ndbm.h
		    DB_LIB=gdbm
		else
		    DB_STYLE=
		    AC_MSG_WARN([*** gdbm database library requested but -lgdm not found.])
		fi
	    else
		if test "$ac_cv_lib_c_dbm_open" = yes; then
		    DB_HEADER=ndbm.h
		    DB_LIB=c
		else
		    DB_STYLE=
		    AC_MSG_WARN([*** gdbm library requested but -lgdm doesn't exist and dbm_open cannot be found.])
		fi
	    fi
	else
	    DB_STYLE=
	    AC_MSG_WARN([*** gdbm database library requested but ndbm.h not found.])
	fi
	;;

    ndbm)
	if test "$ac_cv_header_ndbm_h" = yes; then
	    AC_CHECK_LIB(ndbm,main)
	    if test "$ac_cv_lib_ndbm_main" = yes; then
		AC_CHECK_LIB(ndbm,dbm_open)
		if test "$ac_cv_lib_ndbm_dbm_open" = yes; then
		    DB_HEADER=ndbm.h
		    DB_LIB=ndbm
		else
		    DB_STYLE=
		    AC_MSG_WARN([*** ndbm database library requested but -lgdm not found.])
		fi
	    else
		if test "$ac_cv_lib_c_dbm_open" = yes; then
		    DB_HEADER=ndbm.h
		    DB_LIB=c
		else
		    DB_STYLE=
		    AC_MSG_WARN([*** ndbm library requested but -lndbm doesn't exist and dbm_open cannot be found.])
		fi
	    fi
	else
	    DB_STYLE=
	    AC_MSG_WARN([*** ndbm database library requested but ndbm.h not found.])
	fi
	;;
    text)
	DB_HEADER=
	DB_LIB=
	;;
esac

if test -z "$DB_STYLE" -a "$ac_cv_header_ndbm_h" = yes; then
    AC_CHECK_LIB(ndbm,dbm_open)
    if test "$ac_cv_lib_ndbm_dbm_open" = yes; then
	DB_STYLE=ndbm
	DB_HEADER=ndbm.h
	DB_LIB=ndbm
    elif test "$ac_cv_lib_c_dbm_open" = yes; then
	DB_STYLE=ndbm
	DB_HEADER=ndbm.h
	DB_LIB=c
    fi
fi

if test -z "$DB_STYLE" -a "$ac_cv_header_db_h" = yes; then
    AC_CHECK_LIB(db,dbm_open)
    if test "$ac_cv_lib_db_dbm_open" = yes; then
	DB_STYLE=db
	DB_HEADER=db.h
	DB_LIB=db
    elif test "$ac_cv_lib_c_dbm_open" = yes; then
	DB_STYLE=db
	DB_HEADER=db.h
	DB_LIB=c
    fi
fi

if test -z "$DB_STYLE" -a "$ac_cv_header_dbm_h" = yes; then
    AC_CHECK_LIB(dbm,dbm_open)
    if test "$ac_cv_lib_db_dbm_open" = yes; then
	DB_STYLE=dbm
	DB_HEADER=dbm.h
	DB_LIB=dbm
    elif test "$ac_cv_lib_c_dbm_open" = yes; then
	DB_STYLE=dbm
	DB_HEADER=dbm.h
	DB_LIB=c
    fi
fi

if test -z "$DB_STYLE" -a "$ac_cv_header_ndbm_h" = yes; then
    AC_CHECK_LIB(gdbm,dbm_open)
    if test "$ac_cv_lib_gdbm_dbm_open" = yes; then
	DB_STYLE=gdbm
	DB_HEADER=ndbm.h
	DB_LIB=gdbm
    elif test "$ac_cv_lib_c_dbm_open" = yes; then
	DB_STYLE=gdbm
	DB_HEADER=ndbm.h
	DB_LIB=c
    fi
fi

if test -z "$DB_STYLE"; then
    if test "$ac_cv_lib_c_dbm_open" = yes; then
	AC_MSG_WARN([*** A dbm_open() routine was found in -lc, but no header])
	AC_MSG_WARN([*** files exist.  Please remedy the situation.])
    else
	AC_MSG_WARN([*** No dbm_open() routine found!])
    fi
    AC_MSG_WARN([*** You may want to install the gdbm library from])
    AC_MSG_WARN([*** ftp://prep.ai.mit.edu/pub/gnu/gdbm-1.7.3.tar.gz.])

    AC_MSG_WARN([*** Using the built-in text database.])
    DB_STYLE=text
    DB_HEADER=
    DB_LIB=
fi

if test "$DB_STYLE" = text; then
    AC_DEFINE(TEXTDB)
else
    AC_MSG_CHECKING([for database])
    AC_MSG_RESULT([header is $DB_HEADER, linking against -l$DB_LIB])
    case "$DB_STYLE" in
	db)	AC_DEFINE(USE_DB_H)   ;;
	dbm)	AC_DEFINE(USE_DBM_H)  ;;
	gdbm)	AC_DEFINE(USE_NDBM_H) ;;
	ndbm)	AC_DEFINE(USE_NDBM_H) ;;
    esac

    AC_CACHE_CHECK(
	[for struct datum declared in header files],
	amanda_cv_struct_datum,
	[
	    AC_TRY_COMPILE(
		[
		    #if defined(USE_DB_H)
		    #  include <db.h>
		    #elif defined(USE_DBM_H)
		    #  include <dbm.h>
		    #elif defined(USE_NDBM_H)
		    #  include <ndbm.h>
		    #endif
		],
		[
		    datum a;
		],
		amanda_cv_struct_datum=yes,
		amanda_cv_struct_datum=no
	    )
	])
    if test "$amanda_cv_struct_datum" = yes; then
	AC_DEFINE(HAVE_STRUCT_DATUM)
    fi
fi

case "$DB_STYLE" in
    db) DB_EXT=.db;;
    gdbm) DB_EXT='""';;
    dbm | ndbm) DB_EXT=".dir .pag";;
    text) DB_EXT='""';;
    *) DB_EXT=;;
esac
AC_SUBST(DB_EXT)

dnl Checks for library functions and if the function is declared in
dnl an appropriate header file.  Add some of the missing functions
dnl to LIBOBJS.
ICE_CHECK_DECL(accept,sys/types.h sys/socket.h)
ICE_CHECK_DECL(atof,stdlib.h)
ICE_CHECK_DECL(bind,sys/types.h sys/socket.h)
ICE_CHECK_DECL(bzero,string.h)
ICE_CHECK_DECL(closelog,syslog.h)
ICE_CHECK_DECL(connect,sys/types.h sys/socket.h)
ICE_CHECK_DECL(dbm_open,${DB_HEADER-no/db/header/file})
ICE_CHECK_DECL(fclose,stdio.h)
ICE_CHECK_DECL(fflush,stdio.h)
ICE_CHECK_DECL(flock,sys/file.h)
ICE_CHECK_DECL(fprintf,stdio.h)
ICE_CHECK_DECL(fputc,stdio.h)
ICE_CHECK_DECL(fputs,stdio.h)
ICE_CHECK_DECL(fread,stdio.h)
ICE_CHECK_DECL(fwrite,stdio.h)
AC_REPLACE_FUNCS(getcwd)
AC_CHECK_FUNCS(getfsent)
ICE_CHECK_DECL(gethostname,unistd.h)
AC_FUNC_GETMNTENT
ICE_CHECK_DECL(getopt,stdlib.h unistd.h)
ICE_CHECK_DECL(getpeername,sys/types.h sys/socket.h)
ICE_CHECK_DECL(getsockname,sys/types.h sys/socket.h)
ICE_CHECK_DECL(getsockopt,sys/types.h sys/socket.h)
ICE_CHECK_DECL(gettimeofday,time.h sys/time.h)
AMANDA_FUNC_GETTIMEOFDAY_ARGS
AC_CHECK_FUNCS(getvfsent isascii)
ICE_CHECK_DECL(initgroups,grp.h sys/types.h unistd.h)
ICE_CHECK_DECL(listen,sys/types.h sys/socket.h)
ICE_CHECK_DECL(lstat,sys/types.h sys/stat.h)
ICE_CHECK_DECL(memset,string.h)
AC_CHECK_FUNCS(mkdir)
ICE_CHECK_DECL(mktemp,stdlib.h)
AC_REPLACE_FUNCS(mktime)
ICE_CHECK_DECL(mktime,time.h sys/time.h)
AC_FUNC_MMAP
ICE_CHECK_DECL(openlog,syslog.h)
ICE_CHECK_DECL(pclose,stdio.h)
ICE_CHECK_DECL(perror,stdio.h)
ICE_CHECK_DECL(printf,stdio.h)
ICE_CHECK_DECL(puts,stdio.h)
ICE_CHECK_DECL(recvfrom,sys/types.h sys/socket.h)
ICE_CHECK_DECL(remove,stdio.h)
ICE_CHECK_DECL(rename,stdio.h)
ICE_CHECK_DECL(rewind,stdio.h)
AC_CHECK_FUNCS(rmdir)
ICE_CHECK_DECL(ruserok,netdb.h sys/socket.h)
ICE_CHECK_DECL(select,sys/types.h sys/socket.h sys/select.h time.h sys/time.h)
AMANDA_FUNC_SELECT_ARG_TYPE
ICE_CHECK_DECL(sendto,sys/types.h sys/socket.h)

dnl atexit() is prefered, sunos (maybe others?) define on_exit
AC_CHECK_FUNCS(atexit)
AC_CHECK_FUNCS(on_exit)

dnl arguments for setpgrp or not
AC_CHECK_FUNC(setpgrp,[AC_FUNC_SETPGRP])
ICE_CHECK_DECL(setpgrp,sys/types.h unistd.h)

ICE_CHECK_DECL(setsockopt,sys/types.h sys/socket.h)

AC_CHECK_FUNCS(shmget,
    [
	AMANDA_FUNC_SHM_ARG_TYPE
	case "$FORCE_MMAP" in
	n | no)
	    AC_DEFINE(HAVE_SYSVSHM)
	  ;;
	esac
    ]
)
ICE_CHECK_DECL(shmat,sys/types.h sys/ipc.h sys/shm.h)
ICE_CHECK_DECL(shmctl,sys/types.h sys/ipc.h sys/shm.h)
ICE_CHECK_DECL(shmdt,sys/types.h sys/ipc.h sys/shm.h)
ICE_CHECK_DECL(shmget,sys/types.h sys/ipc.h sys/shm.h)

if test "$ac_cv_func_mmap_fixed_mapped" != yes; then
    case "$FORCE_MMAP" in
    n | no)
	if test "$ac_cv_func_shmget" != yes; then
	    AC_MSG_WARN([*** Neither shmget() nor mmap() found!])
	    AC_MSG_WARN([*** This system will not support the Amanda server.])
	    NO_SERVER_MODE=true
	fi
      ;;
    y | ye | yes)
	AC_MSG_WARN([*** --with-mmap used on a system with no mmap() support!])
	AC_MSG_WARN([*** This system will not support the Amanda server.])
	NO_SERVER_MODE=true
      ;;
    esac
fi

AC_CHECK_FUNCS(sigaction sigemptyset sigvec)
ICE_CHECK_DECL(socket,sys/types.h sys/socket.h)
ICE_CHECK_DECL(socketpair,sys/types.h sys/socket.h)
ICE_CHECK_DECL(sscanf,stdio.h)
AC_CHECK_FUNCS(statfs statvfs)
AC_REPLACE_FUNCS(strerror)
ICE_CHECK_DECL(strerror,string.h)
AC_FUNC_STRFTIME
ICE_CHECK_DECL(strftime,time.h sys/time.h)
ICE_CHECK_DECL(strncasecmp,string.h)
AC_REPLACE_FUNCS(strstr)
ICE_CHECK_DECL(syslog,syslog.h)
ICE_CHECK_DECL(system,stdlib.h)
ICE_CHECK_DECL(time,time.h sys/time.h)
ICE_CHECK_DECL(tolower,ctype.h)
ICE_CHECK_DECL(toupper,ctype.h)
ICE_CHECK_DECL(ungetc,stdio.h)
AC_FUNC_VPRINTF
ICE_CHECK_DECL(vfprintf,stdio.h)
ICE_CHECK_DECL(vsprintf,stdio.h)

dnl disk device prefixes
AC_MSG_CHECKING(disk device prefixes)
dnl Use df to find the mount point for the root filesystem.  Use
dnl the positional parameters to find the particular line from df
dnl that contains the root paritition.  We put it in a subshell so
dnl that the original positional parameters are not messed with.
dfline=`(
    df | while read line; do
	set -- $line
	while test $# -gt 0; do
	    if test "$1" = "/"; then
		echo $line
		break 2
	    fi
	    shift
	done
    done
) | sed 's/(//' | sed 's/)//' `

dnl Search for the mount point by using expr to find the parameter
dnl with dev in it.
mount=`(
    set -- $dfline
    while test $# -gt 0; do
	if expr "$1" : '.*dev' >/dev/null 2>&1; then
	    echo $1
	    break
	fi
	shift
    done
)`

if test "$DEV_PREFIX" && test "$RDEV_PREFIX"; then
    AC_MSG_RESULT((predefined) $DEV_PREFIX - $RDEV_PREFIX)
else
    if test -d /dev/dsk; then
	DEV_PREFIX=/dev/dsk/
	if test -d /dev/rdsk; then
	    RDEV_PREFIX=/dev/rdsk/
	else
	    RDEV_PREFIX=/dev/dsk/
	fi
    elif test -d /dev; then
	DEV_PREFIX=/dev/
	# Some systems, notably Linux, do not have raw disk devices
	# names.  Check this by trying to see if a raw disk device name
	# exists using the normal raw device path prepended to the
	# mount point of the root filesystem.
	if test "$mount"; then
	    dev_name="/dev/r`basename $mount`"
	    if test -b $dev_name -o -c $dev_name; then
		RDEV_PREFIX=/dev/r
	    else
		RDEV_PREFIX=/dev/
	    fi
	else
	    RDEV_PREFIX=/dev/r
	fi
    else
	DEV_PREFIX=/
	RDEV_PREFIX=/
    fi
    AC_MSG_RESULT($DEV_PREFIX - $RDEV_PREFIX)
fi

AC_DEFINE_UNQUOTED(DEV_PREFIX,"${DEV_PREFIX}")
AC_DEFINE_UNQUOTED(RDEV_PREFIX,"${RDEV_PREFIX}")

AC_MSG_CHECKING(whether /dev/root and /dev/rroot exist)
if test -b /dev/root -a -c /dev/rroot; then
    AC_DEFINE_UNQUOTED(DEV_ROOT)
    AC_MSG_RESULT(yes)
else
    AC_MSG_RESULT(no)
fi

case $mount in
    /dev/vg*)
	AC_MSG_WARN("*** Run amhpfixdevs on HP-UX systems using /dev/vg??.")
	;;
esac

dnl maximum file size on holding disk
AC_CACHE_CHECK(
    [maximum file size],
    amanda_cv_max_file_size,
    [
	amanda_cv_max_file_size=0
	rm -f conftest.$ac_ext
	cat <<EOF >conftest.$ac_ext
#include "confdefs.h"
#include <stdio.h>
#include <limits.h>
#ifdef HAVE_SYS_TYPES_H
# include <sys/types.h>
#endif
#include <sys/uio.h>

int main()
{
    struct iovec test;
    unsigned long compare = 0, last = 0;

    test.iov_len = 1;
    while ((test.iov_len > 0) && (test.iov_len > last)) {
	if ((last = test.iov_len) > compare)
	    compare = test.iov_len;
	test.iov_len = (test.iov_len << 1) | 0x01;
    }
    printf("%ld\n", compare / 1024);
    return 0;
}
EOF
	${CC-cc} -o conftest $CFLAGS $CPPFLAGS $LDFLAGS conftest.$ac_ext $LIBS >/dev/null 2>/dev/null
	if test $? = 0; then
	    MAXFILESIZE=`./conftest`
	    STATUS=$?
	    if test "$STATUS" = 0; then
		amanda_cv_max_file_size=$MAXFILESIZE
	    fi
	fi
	rm -f conftest*
    ]
)
if test "$amanda_cv_max_file_size" -gt 0; then
    AC_DEFINE_UNQUOTED(MAXFILESIZE,$amanda_cv_max_file_size)
fi
dnl		AC_MSG_RESULT($amanda_cv_max_file_size kB)
dnl	else
dnl		AC_MSG_RESULT(undefined ($STATUS))
dnl	fi
dnl else
dnl	AC_MSG_RESULT(undefined (compiler error))
dnl fi

dnl lock/flock/fcntl
AC_CACHE_CHECK(
   [whether posix fcntl locking works],
   amanda_cv_posix_filelocking,
   [
	rm -f conftest.$ac_ext
	cat <<EOF >conftest.$ac_ext
#include "confdefs.h"

#define CONFIGURE_TEST
#define USE_POSIX_FCNTL
#include "${srcdir-.}/common-src/amflock.c"
EOF
	${CC-cc} -o conftest $CFLAGS $CPPFLAGS $LDFLAGS conftest.$ac_ext $LIBS >/dev/null 2>/dev/null
	if test $? = 0; then
	    ./conftest
	    STATUS=$?
	    if test "$STATUS" = 0; then
		amanda_cv_posix_filelocking=yes
	    else
		amanda_cv_posix_filelocking="no (test returned ${STATUS})"
	    fi
	else
	    amanda_cv_posix_filelocking="no (compiler error)"
	fi
	dnl cp conftest.c fcntltest.c
	rm -f conftest* /tmp/conftest.lock
    ]
)
if test "$amanda_cv_posix_filelocking" = yes; then
    AC_DEFINE(USE_POSIX_FCNTL)
    HAS_WORKING_FILE_LOCK=1
fi

if test -z "$HAS_WORKING_FILE_LOCK"; then
    AC_CACHE_CHECK(
	[whether flock locking works],
	amanda_cv_flock_filelocking,
	[
	    rm -f conftest.$ac_ext
	    cat <<EOF >conftest.$ac_ext
#include "confdefs.h"

#if defined(LOCK_EX) && defined(LOCK_UN)

#define CONFIGURE_TEST
#define USE_FLOCK
#include "${srcdir-.}/common-src/amflock.c"

#else

main()
{
    exit(42);
}

#endif
EOF
	    ${CC-cc} -o conftest $CFLAGS $CPPFLAGS $LDFLAGS conftest.$ac_ext $LIBS >/dev/null 2>/dev/null
	    if test $? = 0; then
		./conftest
		STATUS=$?
		if test "$STATUS" = 0; then
		    amanda_cv_flock_filelocking="yes"
		else
		    amanda_cv_flock_filelocking="no ($STATUS)"
		fi
	    else
		amanda_cv_flock_filelocking="no (compiler error)"
	    fi
	    dnl cp conftest.c flocktest.c
	    rm -f conftest* /tmp/conftest.lock
	]
    )
    if test "$amanda_cv_flock_filelocking" = yes; then
	AC_DEFINE(USE_FLOCK)
	HAS_WORKING_FILE_LOCK=1
    fi
fi

if test -z "$HAS_WORKING_FILE_LOCK"; then
    AC_CACHE_CHECK(
	[whether lockf locking works],
	amanda_cv_lockf_filelocking,
	[
	    rm -f conftest.$ac_ext
	    cat <<EOF >conftest.$ac_ext
#include "confdefs.h"

#define CONFIGURE_TEST
#define USE_LOCKF
#include "${srcdir-.}/common-src/amflock.c"
EOF
	    ${CC-cc} -o conftest $CFLAGS $CPPFLAGS $LDFLAGS conftest.$ac_ext $LIBS >/dev/null 2>/dev/null
	    if test $? = 0; then
		./conftest
		STATUS=$?
		if test "$STATUS" = 0; then
		    amanda_cv_lockf_filelocking="yes"
		else
			amanda_cv_lockf_filelocking="no ($STATUS)"
		fi
	    else
		amanda_cv_lockf_filelocking="no (compiler error)"
	    fi
	    dnl cp conftest.c lockftest.c
	    rm -f conftest* /tmp/conftest.lock
	]
    )
    if test "$amanda_cv_lockf_filelocking" = yes; then
	AC_DEFINE(USE_LOCKF)
	HAS_WORKING_FILE_LOCK=1
    fi
fi

if test -z "$HAS_WORKING_FILE_LOCK"; then
    AC_CACHE_CHECK(
	[whether mylock locking works],
	amanda_cv_mylock_filelocking,
	[
	    rm -f conftest.$ac_ext
	    cat <<EOF >conftest.$ac_ext
#include "confdefs.h"

#define CONFIGURE_TEST
#define USE_MYLOCK
#include "${srcdir-.}/common-src/amflock.c"

void *alloc(l)
int l;
{
    return malloc(l);
}
EOF
	    ${CC-cc} -o conftest $CFLAGS $CPPFLAGS $LDFLAGS conftest.$ac_ext $LIBS >/dev/null 2>/dev/null
	    if test $? = 0; then
		./conftest
		STATUS=$?
		if test "$STATUS" = 0; then
		    amanda_cv_mylock_filelocking="yes"
		else
			amanda_cv_mylock_filelocking="no ($STATUS)"
		fi
	    else
		amanda_cv_mylock_filelocking="no (compiler error)"
	    fi
	    dnl cp conftest.c mylocktest.c
	    rm -f conftest* /tmp/conftest.lock
	]
    )
    if test "$amanda_cv_mylock_filelocking" = yes; then
	AC_DEFINE(USE_MYLOCK)
	HAS_WORKING_FILE_LOCK=1
    fi
fi

if test -z "$HAS_WORKING_FILE_LOCK"; then
    AC_MSG_WARN([*** No working file locking capability found!])
    AC_MSG_WARN([*** Be VERY VERY careful.])
fi

dnl restore the old CFLAGS
CFLAGS="$OLD_CFLAGS"
LIBS="$LIBS $KRB4LIBS"

dnl extra substitution parameters
AC_SUBST(MTX)
AC_SUBST(ac_n)
AC_SUBST(ac_c)

dnl kerberos bits that need to be linked into common-src/libamanda.a
AC_SUBST(KRB4COMMON_C)
AC_SUBST(KRB4COMMON_O)
AC_SUBST(KRB4CLIENT_C)
AC_SUBST(KRB4CLIENT_O)

if ${NO_SERVER_MODE-false}; then
    BUILD_CHANGER_PROGS_LIBEXEC=
    BUILD_CHANGER_SCRIPTS_LIBEXEC=
    BUILD_SERVER_PROGS_SBIN=
    BUILD_SERVER_PROGS_LIBEXEC=
    BUILD_SERVER_SCRIPTS_SBIN=
    BUILD_SERVER_LIB=
    BUILD_AMPLOT_SBIN=
    BUILD_AMPLOT_LIBEXEC=
fi

if ${NO_CLIENT_MODE-false}; then
    BUILD_CLIENT_PROGS_LIBEXEC=
    BUILD_CLIENT_SCRIPTS_LIBEXEC=
    BUILD_CLIENT_LIB=
    BUILD_RECOVER_PROGS_SBIN=
fi

dnl for libtool support on autoconf 2.12 and earlier
LTLIBOBJS=`echo "$LIBOBJS" | sed 's/\.o/\.lo/g'`
AC_SUBST(LTLIBOBJS)

AC_OUTPUT( \
	common-src/versuff.c		common-src/Makefile		\
									\
	client-src/patch-system.sh	client-src/Makefile		\
									\
	restore-src/Makefile						\
									\
	tape-src/Makefile						\
									\
	server-src/amcheckdb.sh		server-src/amcleanup.sh		\
	server-src/amdump.sh		server-src/amfreetapes.sh	\
	server-src/amoverview.pl	server-src/amrmtape.sh		\
	server-src/amtoc.pl		server-src/amverify.sh		\
	server-src/Makefile						\
									\
	changer-src/chg-generic.sh	changer-src/hp-changer.sh	\
	changer-src/no-changer.sh	changer-src/rth-changer.pl	\
	changer-src/Makefile						\
									\
	recover-src/Makefile						\
									\
	amplot/amcat.awk		amplot/amplot.sh		\
	amplot/Makefile							\
									\
	example/amanda.conf		example/Makefile		\
									\
	man/amadmin.8			man/amanda.8			\
	man/amcheck.8			man/amcleanup.8			\
	man/amdump.8			man/amflush.8			\
	man/amlabel.8			man/Makefile			\
									\
	docs/Makefile							\
	patches/Makefile						\
	config-aux/Makefile						\
									\
	Makefile)
